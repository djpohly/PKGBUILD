# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>
# Contributor: Clément Démoulins <clement@archivel.fr>

_pkgname=tt-rss
pkgname=$_pkgname-jjk
pkgver=1.7.4
pkgrel=2
pkgdesc="Open source web-based news feed (RSS/Atom) aggregator"
arch=('any')
url="http://tt-rss.org/redmine/"
license=('GPL')
depends=('php')
optdepends=('mysql' 'postgresql' 'php-curl')
options=('!strip')

source=(http://tt-rss.org/download/$_pkgname-$pkgver.tar.gz
        tt-rss.service
        0001-Remove-open_basedir-limitation.patch)
md5sums=('9ce5af16274fd79ab66cc145834a1845'
         '8ac0d34c0bcfef0f7c1205d169ee8415'
         '175de37c2b42cb78372adf795f5b5b35')

build() {
    cd "$srcdir/$_pkgname-$pkgver"
    patch -p1 -i ../0001-Remove-open_basedir-limitation.patch
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"
    _instdir=${pkgdir}/usr/share/webapps/${_pkgname}

    # install tt-rss
    mkdir -p ${_instdir}
    cp -ra * ${_instdir}/
    rm -rf ${_instdir}/debian

    # add a daemon
    install -D -m644 ${srcdir}/tt-rss.service ${pkgdir}/usr/lib/systemd/system/tt-rss.service

    # make several directories writable by http user
    chown -R http:root ${_instdir}/{lock,feed-icons,cache}
}

