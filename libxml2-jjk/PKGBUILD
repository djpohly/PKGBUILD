# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>

_pkgname=libxml2
pkgname=$_pkgname-jjk
pkgver=2.9.4
pkgrel=1
pkgdesc="XML parsing library, version 2"
arch=('i686' 'x86_64')
license=('MIT')
depends=('zlib' 'readline' 'ncurses' 'xz')
options=('!makeflags')
makedepends=('python2' 'python')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
url="http://www.xmlsoft.org/"
source=(ftp://xmlsoft.org/libxml2/libxml2-$pkgver.tar.gz{,.asc}
        https://www.w3.org/XML/Test/xmlts20080827.tar.gz
        libxml2-2.9.4-remove-pyverify_fd.patch)
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
sha256sums=('ffb911191e509b966deb55de705387f14156e1a56b21824357cdf0053233633c'
            'SKIP'
            '96151685cec997e1f9f3387e3626d61e6284d4d6e66e0e440c209286c03e9cc7'
            'c201b0d6364a7b9df58ce40a5fc506e58945a3fbb834af9c1193a35665789d60')

prepare() {
  mkdir build-py3
  mkdir build-py2
  mv xmlconf build-py2/
  cd $_pkgname-$pkgver
  patch -Np1 -i ../libxml2-2.9.4-remove-pyverify_fd.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd build-py2
  ../$_pkgname-$pkgver/configure --prefix=/usr --with-threads --with-history \
      --with-python=/usr/bin/python2
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make

  cd ../build-py3
  ../$_pkgname-$pkgver/configure --prefix=/usr --with-threads --with-history \
      --with-python=/usr/bin/python
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  PYTHONHASHSEED=0 make
}

check() {
  cd build-py2
  make check
}

package() {
  cd build-py2
  find doc -type f -exec chmod 0644 {} \;

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"

  cd ../build-py3/python
  PYTHONHASHSEED=0 make DESTDIR="$pkgdir" install
}
