# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>

_pkgname=dovecot
pkgname=$_pkgname-jjk
pkgver=2.2.21
pkgrel=1
pkgdesc="An IMAP and POP3 server written with security primarily in mind"
arch=('i686' 'x86_64')
url="http://dovecot.org/"
license=("LGPL")
depends=('bzip2' 'expat' 'curl')
makedepends=('libcap')
provides=($_pkgname 'imap-server' 'pop3-server')
conflicts=($_pkgname)
install=$_pkgname.install
source=(http://dovecot.org/releases/2.2/${_pkgname}-${pkgver}.tar.gz{,.sig}
        dovecot.tmpfilesd)
validpgpkeys=('E643F0BDFDCD04D9FFCB6279C948525140558AC9')
md5sums=('28c39ab78a20f00701c26960d9190cf0'
         'SKIP'
         '342a28251d40f983c98c0d1f1bf3d07d')

build() {
    cd ${srcdir}/$_pkgname-$pkgver

    ./configure --prefix=/usr --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --libexecdir=/usr/lib \
        --with-moduledir=/usr/lib/dovecot/modules \
        --with-systemdsystemunitdir=/usr/lib/systemd/system \
        --disable-static \
        --with-zlib --with-bzlib \
        --with-libcap \
        --with-solr \
        --with-docs
    make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -k check
}

package() {
    cd ${srcdir}/$_pkgname-$pkgver
    make DESTDIR=${pkgdir} install

    # install example conf files
    install -d -m755 ${pkgdir}/etc/dovecot/conf.d
    install -m 644 ${pkgdir}/usr/share/doc/dovecot/example-config/dovecot.conf ${pkgdir}/etc/dovecot/dovecot.conf.sample

    rm ${pkgdir}/etc/dovecot/README

    # systemd tmpfile
    install -d -m755 ${pkgdir}/usr/lib/tmpfiles.d
    install -m 644  ${srcdir}/dovecot.tmpfilesd ${pkgdir}/usr/lib/tmpfiles.d/dovecot.conf
}
