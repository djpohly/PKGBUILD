# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

_pkgname=exo
pkgname=$_pkgname-jjk
pkgver=0.10.7
pkgrel=1
pkgdesc="Extensions to Xfce by os-cillation"
arch=('i686' 'x86_64')
url="https://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui' 'gtk2' 'perl-uri' 'hicolor-icon-theme')
makedepends=('intltool')
provides=($_pkgname)
conflicts=($_pkgname)
source=(http://archive.xfce.org/src/xfce/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2)
sha256sums=('521581481128af93e815f9690020998181f947ac9e9c2b232b1f144d76b1b35c')

build() {
  cd "$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
