# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.8.4
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool' '!strip' 'debug')
backup=(etc/gtk-3.0/settings.ini)
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-tooltip-Fix-possible-wrong-placement.patch
        0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
        0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
        0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
        0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
        0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('b08360167702299e91b3435a4ce6bae41dd26d2794e898632f12dd091b0692e2'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            'eeafa8688b74f40d886ba2c66fb1e20352831353e92901b03e87a7f3379c8a44'
            '04a40393c98ebaf183b379a7098f3530308f67c89d2ddce43501e0fc6384c46c'
            'ce6ffb6d37cb73eb294b8c0a4631a53113e1e664da30b375a66792f24bce8ade'
            'b8079f0f3f1cd34894e785013244d652dcd2eca63622c30fac0828ba03f7a17b'
            'c8f5017de9c651db85144f58a2ccb3eefdc0c60fb17721ce6963428ed0c9efcb'
            '9bbdc895699c2499f1bf1ab9a01dc56ff89707fff7e7f25be23b90d01c545776'
            '6e41b387fd496ed5cf7d2b3222832cae58374e1fd90431947e942b081e89e669'
            '70f3b43c1f7acac083028f8e1ca5426c4d95a1008f091d8e3713ff3f3f4a506f'
            'bbe8db16ae5ca4333c9ef4c777569a1f0df9324da3f6ffd88f8caa3f6caa2636'
            '64679fcefa6fc3777d31d228b516fae878bb13c17a695c2798fa050bc1e7ddce'
            '9a47bead9905d60e34f9d593d9ad85cfec90850c30aa58a1b36fefb8a8b1b552'
            '670e3c9c78f3444158c34adf555da9c7b09c3a49efeab295c4815d980f4260b6'
            '2e0a3503a0c420eb0283c97adc56e188ac9e42530d79edb3dfaacc7dec2abef8'
            'dd702e29440c8b3d19daa3a209d1df98c847ffd83efa59700f5a100917ac7b9f'
            'b97b865d11609088f7a978050301cbb502c26b016996389742684323321f5dea'
            '75fd328c66a3cf829cb2b7b5cffe065026e10baea7bb360e3ca7ee5434178bb4'
            'cfabfbe4ee529eb95fca3f85d651db50b0d9d2b3c822064802bdef18325740ff')
prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
    patch -p1 -i ../0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-tooltip-Fix-possible-wrong-placement.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
    patch -p1 -i ../0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
    patch -p1 -i ../0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch

    ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
