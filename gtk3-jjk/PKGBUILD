# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.8.1
pkgrel=4
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool' '!strip' 'debug')
backup=(etc/gtk-3.0/settings.ini)
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-Revert-window-restore-size-after-hide-show-properly.patch
        0002-Fix-696882-window-would-revert-to-minimum-size-after.patch
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-tooltip-Fix-possible-wrong-placement.patch
        0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
        0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('f43e7c7d7a0e2417225509e88f67799534c69abfbb6f615fc7a895bf15e31255'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            '669c39baeb765fb9a99daf0fb8d08358d1a2baf3f66f8c45c46ec9b75839f3e4'
            '43eb3775c935052541300adf550f41fdf2a25e46e2612fdb357cf1ec1ebd02f4'
            '1954fc7a24591776a482069c6b379916f78de83832f9e825cd8695640fc09d26'
            'fcd6ce586910fba9fd670cac9540bb44fd25d904f040bf16c3254fe0748b22f3'
            '19b8d290cda0fa3210f6db495e13998b53a345054e67c22e8af31c92507003c1'
            '59757cc89469444956d029a510e394c86ea148f3e299b8bf5c2731ac8c4d7d9b'
            '25aede6a172bd641e7775cd08cebc9b2e17cf82c8e2ab5e73714d2959d87851a'
            'e78abd8173761eb23e1733619fb332c20665b7be0d60243d0f3a1cbb07522f3c')
prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
    patch -p1 -i ../0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-tooltip-Fix-possible-wrong-placement.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch

    # official fix is actually buggy, as seen in original report:
    # https://bugzilla.gnome.org/show_bug.cgi?id=696882
    # it also means a window with an expander will get its default size
    # forcefully restore when toggling the expander, thus undoing any manual
    # resize
    # So as we wait to see how upstream fixes that, revert to our fix, which
    # (seems to) just fix the original bug :)
    patch -p1 -i ../0001-Revert-window-restore-size-after-hide-show-properly.patch
    patch -p1 -i ../0002-Fix-696882-window-would-revert-to-minimum-size-after.patch
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
