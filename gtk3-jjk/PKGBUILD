# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.16.6
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libepoxy libxcomposite libxdamage pango shared-mime-info
         at-spi2-atk adwaita-icon-theme)
makedepends=(gobject-introspection mesa gtk-doc)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-Add-GTK_IS_JJK.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-fix-typo.patch
        0001-menu-Fix-possible-leak-on-destroy.patch
        0001-revert-changes-to-GtkMessageDialog.patch
        0001-Revert-treeview-Store-editable-position-differently.patch
        0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
        0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
        0001-treemodel-Fix-gtk_tree_path_up-making-path-invalid.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-treeview-Add-signal-test-rubber-banding.patch
        0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
        0001-treeview-Better-resizing-with-expand-columns.patch
        0001-treeview-Fix-only-headers-being-refreshed-on-validat.patch
        0001-treeview-Fix-possible-column-rendering-issue.patch
        0001-treeview-Fix-prelight-re-gesture-refactoring.patch
        0001-treeview-Remove-some-dead-code.patch
        0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0002-treeview-Add-signal-rubber-banding-active-and-gtk_tr.patch
        0002-treeview-Fix-visual-glitch-on-row-inserted.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0004-treeview-Add-button-in-test-rubber-banding.patch
        0005-treeview-Add-skip_next_button_press.patch
        fix-708148.patch)
sha256sums=('4d12726d0856a968b41802ae5c5971d7e9bac532717e309d3f81b9989da5ffbe'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            'f8f84600c47df5b41decd8a0df969f46e643eed7d2e17797cb21929ea30eda42'
            'e36308258632a960d11a0eeb0ad9e24bfa5684d2abc87ba0cdc4f8161c549e0c'
            '11a4f4b0c37f6f422ef3c311e13b6794f753a1fd1e5e5512f1af85d4922c09c2'
            'c3ef5fe1d73bea2cf9f65cf14d23704db395f99010e06ad68547f7661e03569f'
            '7123837dc2ca82245bf20fa32407f9c778a026cd5ed9cba01de07eeb70d54d5a'
            '194b79b00ac79545cc791a47b6840adf6d692e5bbcf2b90a29ae7e5e4ca05fd3'
            '32a1b546996d936dd52bd12aea72d6b8ac4a4232a56f56b9512ea43714d2b990'
            'dbb12ad1f873f7898ec2054978c6e6447869bcb3ee37709ed76f7b816ccee8d2'
            '336aaf3a9dc2fda4eb316d4533051d734b56703aa44b5f12a64fc3842b0ae96a'
            '33057f04d7e3456cfca7d9a9c8e1347daffc59bdeb901b9c4aea75851bba631e'
            '3b7e0e9596e7c03df0b35dfb4ced9ba53c3d8ef43bbc1f971432f610f4535020'
            'cd6d3e61c3f9366e4ff96231e14b18cae40d0be9a41ddf0d52867d70a42735a3'
            '308743f04f6d4ef73157b324ca682316607d85e61ee88f7559cde274765a508f'
            '81f0412684bd0dab62504991e50590bb6a3a6dc55d95208e7228d450787347a6'
            '2e403a2c7a0deb554a9c9ec5a19e20a348a8f7f71c999101fd61c11ee18d118c'
            '494bf08a6dc7759bf016ada138cfc44293f9ab69333074daae5a04ad39764c28'
            'b851a3b099cc4fd7e9b0ea21040aed0a6bc3880e291f1b0ac885d0c3be2efecb'
            'e65b3dc11ef575a2a9912f7ee0f170bbe1f9bb06c88f297db3207f9b26d099db'
            '0baa9c63b40a82f9bad4136278df0b900864a341d9020adcc7910fcfa308439d'
            'baa0722a72c4a32969f0d40ef8cde8601e5a95f39d73c52e07551ffd058a5a6e'
            '2a5d169f5cd108915514f044780940f8561626dbea127b0095280732c51def64'
            'd549963691698c509b33c565ad415c95bcad3935b75ea4d726b5d11a588feaf0'
            '71f7acf986109a15e9d7cd15744dbdad2fbc1268d7f3065a32886926298a73c0'
            'bc7aa63b61d2c9c0babe7bf5294659c3f1ac6a7275fa93b94c572edc6f0b9e35'
            'bb5fcb5b7ce80041b358638f956f2e2d1716c65a90025cc4385b9deabfd95c53'
            '58fafe07e340c81e5f99fa7791f583952425a36aa4a797700b41301dd3e0975e'
            '55b1cc965c015764c9c0a5498a8c065207cf6cf26dd20574eaa042415ba15717'
            'f63ee60a01eb1ba8a3535f5fc811fe831bb1c4acea1cef201449684a82d0818c'
            '5f980fac44f17b88c3836aeaa850d610ece1b5e3f67fffb777157ebca0e51d49'
            '8a8b500f6850466b646f788ee45756bbba21dea6f75babddca1d4b32ea1b0b03')

prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-fix-typo.patch
    patch -p1 -i ../0001-menu-Fix-possible-leak-on-destroy.patch
    patch -p1 -i ../0001-revert-changes-to-GtkMessageDialog.patch
    patch -p1 -i ../0001-Revert-treeview-Store-editable-position-differently.patch
    patch -p1 -i ../0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
    patch -p1 -i ../0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
    patch -p1 -i ../0001-treemodel-Fix-gtk_tree_path_up-making-path-invalid.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-treeview-Add-signal-test-rubber-banding.patch
    patch -p1 -i ../0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
    patch -p1 -i ../0001-treeview-Better-resizing-with-expand-columns.patch
    patch -p1 -i ../0001-treeview-Fix-only-headers-being-refreshed-on-validat.patch
    patch -p1 -i ../0001-treeview-Fix-possible-column-rendering-issue.patch
    patch -p1 -i ../0001-treeview-Fix-prelight-re-gesture-refactoring.patch
    patch -p1 -i ../0001-treeview-Remove-some-dead-code.patch
    patch -p1 -i ../0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0002-treeview-Add-signal-rubber-banding-active-and-gtk_tr.patch
    patch -p1 -i ../0002-treeview-Fix-visual-glitch-on-row-inserted.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
    patch -p1 -i ../0004-treeview-Add-button-in-test-rubber-banding.patch
    patch -p1 -i ../0005-treeview-Add-skip_next_button_press.patch
    patch -p1 -i ../fix-708148.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install
    rm -f "$pkgdir/usr/bin/gtk-update-icon-cache"
}
