# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.8.0
pkgrel=3
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool' '!strip' 'debug')
backup=(etc/gtk-3.0/settings.ini)
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-Unreferencing-a-GtkPlug-could-cause-abort-Fix-697427.patch
        0001-Fix-696882-window-would-revert-to-minimum-size-after.patch
        0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
        0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('db1345d89d6b5f5bad411b4b2a3b1a18a2a8f98d2ab1b40d43a0197a6e6c252e'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            'c600dc18df6cb8773e4c0ff7619f6a5a41f3c419f23bcd7af2ea3554cbd29bd7'
            '7fc6c5e61eb03e1e4ec3e2e820be3b42bc30ce324fff12c0c59b6c06ac69099e'
            '474e828c3c6b53c3ee0a442713eb0fa507ce141872240bf23fd539febe32a8e7'
            'c2dd7646287227d70e711841641446c454cab9165a9fa553dd94b16f81ee9f55'
            '644098a937d6065d99cef5ce442480db3ba85394170632df02ceceadf6844ac8')
build() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
    patch -p1 -i ../0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-Fix-696882-window-would-revert-to-minimum-size-after.patch
    patch -p1 -i ../0001-Unreferencing-a-GtkPlug-could-cause-abort-Fix-697427.patch

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
