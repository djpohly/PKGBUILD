# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.8.2
pkgrel=6
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool' '!strip' 'debug')
backup=(etc/gtk-3.0/settings.ini)
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-Workaround-bug-in-drawing-multiline-selection-label-.patch
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-tooltip-Fix-possible-wrong-placement.patch
        0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
        0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
        0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('1ca80c9c15a1df95d74cefb8c2afe4682ba272a4b489106f04877be2a7aff297'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            '44d2a91a1782af53c1092a63771512d55f5c43bd990ae28288ce9d9972ddaecc'
            '031a032224743cda94e26684235683486d5d11e84eab9cd8149ba8937f322743'
            '954a7f991f64c00a6ae9b4dca92e76d5ed6b50b19c7c40ea8ad14e39c01b3148'
            '71ea9e2b6834b210a0b1ff8c44968d1d1188e7e09bfdef50fbc9ff819c05da20'
            'dddc3b5c1a9b83f94cb5dfc4e734896f9239b0025f0085b7041fc7ded7ab0315'
            'ac0e271d5a16a0ac7dab308e09788dbde594eeb589786e4ca3f8224c8be6666b'
            '780d799d6541e1bb0e4fe6b80ce0cd28cba1fcd5f1106935ac85fc9d7e5fdca0'
            'c4a76a9bde219858b5e1f286ee9fce0f7a66eb931505752cb35970c66609a231'
            '5fb93e3c4505865037ba46602fd79403cdc809f44dddcca8d225d6db0339318c'
            '493825e16cd0e06ed9f482fc1e905155e24ec7e39bf2753727e8c0fd9cc90d93'
            'f61aca4ec214a37063a10d671a7f5b863ca02bc107ba9cdb7ac2076f1458d4a0'
            '4b4cdc7bbcf3ba55f2ec87a80684d60e0045b473e1da46f4f07f7eff336e1974'
            'd0066f63d2fc0932931811df3cfa8e0d39354d50269311bcdad832f7eabbe5f3'
            '474e6cdbe7f4eca28d228dc886ac3798f6441cfe3c334e9e6125ac2465557554'
            '289c81547bb52da1e38411f9a48b19981b6e821edcba712ddee978825a2bb894')
prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-Workaround-bug-in-drawing-multiline-selection-label-.patch
    patch -p1 -i ../0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
    patch -p1 -i ../0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-tooltip-Fix-possible-wrong-placement.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
    patch -p1 -i ../0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch

    ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
