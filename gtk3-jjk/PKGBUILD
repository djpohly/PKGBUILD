# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.16.2
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libepoxy libxcomposite libxdamage pango shared-mime-info
         at-spi2-atk adwaita-icon-theme)
makedepends=(gobject-introspection mesa gtk-doc)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-Add-GTK_IS_JJK.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-fix-typo.patch
        0001-menu-Fix-possible-leak-on-destroy.patch
        0001-revert-changes-to-GtkMessageDialog.patch
        0001-Revert-treeview-Store-editable-position-differently.patch
        0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
        0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
        0001-treemodel-Fix-gtk_tree_path_up-making-path-invalid.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-treeview-Add-signal-test-rubber-banding.patch
        0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
        0001-treeview-Better-resizing-with-expand-columns.patch
        0001-treeview-Fix-only-headers-being-refreshed-on-validat.patch
        0001-treeview-Fix-possible-column-rendering-issue.patch
        0001-treeview-Fix-prelight-re-gesture-refactoring.patch
        0001-treeview-Remove-some-dead-code.patch
        0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0002-treeview-Add-signal-rubber-banding-active-and-gtk_tr.patch
        0002-treeview-Fix-visual-glitch-on-row-inserted.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0004-treeview-Add-button-in-test-rubber-banding.patch
        0005-treeview-Add-skip_next_button_press.patch
        fix-708148.patch)
sha256sums=('a03963a61c9f5253a8d4003187190be165d92f95acf97ca783735071a8781cfa'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            '558a1a3857d47d7dac331765f6ae06d5d048c10cde06bfb55266ccce833eb6b1'
            '0738aeceefae3c4550603f08e6dadb0fc019bbfbabd549fbb0b025947364a0da'
            '2555ee406c1044306230a3dbb60b734a193de54024bb0a91904eba1c18abce0f'
            '780cc552bf637aaac8ca383ffb0e6df149b93278c42b338e5b90aad59814f6b0'
            'f0546b425a9ebb94aa153dfe66d15b766535427a35d656fbe1c0b60fbdc5fccb'
            '0e9d7c56c7a4cc7df4cccb81628b54e566965ba899c5de3ace5c25a54142196d'
            '60ac00599dab430526117671ec153ac6ad847c7e4e2d6508252760fc2e0873b8'
            '832e0d81c654fbb8e34c49bf45f48f94f04637489d179edddb133520602e7895'
            '7b301d04297e0abf9c3b20b8e1e546908450cf39dc886a8dd3c284424ae1bea9'
            'c889c56def89b04f5f2c60ba01326df794ae707d8a008e8240ec271d0e670d75'
            '762cc87660bd7dbe63db689a0596ddff25bf4d718c7df5b2a72a26eb285847d5'
            'b8adbf593d945e71451b07bab72f3f193f68de9f40063c619a52186b572b08a3'
            '7ebd059ae26c2099bcd21ddc8bc945277444b10687cb10e257d990c20879a7a3'
            '309d0e908b8b968c8b4da9f93301f1acf609cef53292ab2b4e1bcdad75a2aad8'
            'e73e5b3590d61389e71ae01c30b844fd70512ad9845831155e37c080ebf68700'
            '62b1923b43c76cb61bf9837bc700f67ddb4257661d83232beea4764d72409166'
            '75b0b679cec6097c563a6adaafa979b4633bf9ae2ed409ca1f58c4b3309f0683'
            '0ec5c17a7dc273690ca996fb1eadc8745fce8ca9f0715d2013d5be5348b5931e'
            'c8b511cb21a5d22fb823e8781f0d87971d697cc6d89f2547fc3e6459cdbe3128'
            '01791ab8d29851d9b8c56f98dfc6c2a21e8434764336d672cf7996233813e91f'
            '6faf3dca914e6d6dee766ec330b68a7978d62dbcc0860efa48360aa293dc1430'
            '7337596747d202ce22ff831c7bd9d07e0b523221772547fc337e4a8c4544579b'
            '7a4885cea02862423c012dcc9b089ec70ebcea3c2770d339070b73c17ea00425'
            '50de461d2cd2ff11740a54038bf377b563f8a1f9100a73cc4c8f2121e2d8c30d'
            '2d63a33472c7cc26ba2780741695864439a952c6d9b859554a42e3d76d8a7130'
            '1a54c3fa4314445c33a10a15d59cefc56c7aeed281e48b510f8ea7af3e67cc9e'
            'e18114b143a9761010e851f5df7f2aa82a2856779d6b4f93d661d1b888bbff97'
            '615e128cc3b5610cb8a12c33764ef62fcc12b25fc7b0dd50e1cb791c44a88b88'
            '9f5085bb6b8031af4fa1eea0575a5236195ecd914dd32e145f13df738644f036'
            '8a8b500f6850466b646f788ee45756bbba21dea6f75babddca1d4b32ea1b0b03')

prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-fix-typo.patch
    patch -p1 -i ../0001-menu-Fix-possible-leak-on-destroy.patch
    patch -p1 -i ../0001-revert-changes-to-GtkMessageDialog.patch
    patch -p1 -i ../0001-Revert-treeview-Store-editable-position-differently.patch
    patch -p1 -i ../0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
    patch -p1 -i ../0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
    patch -p1 -i ../0001-treemodel-Fix-gtk_tree_path_up-making-path-invalid.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-treeview-Add-signal-test-rubber-banding.patch
    patch -p1 -i ../0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
    patch -p1 -i ../0001-treeview-Better-resizing-with-expand-columns.patch
    patch -p1 -i ../0001-treeview-Fix-only-headers-being-refreshed-on-validat.patch
    patch -p1 -i ../0001-treeview-Fix-possible-column-rendering-issue.patch
    patch -p1 -i ../0001-treeview-Fix-prelight-re-gesture-refactoring.patch
    patch -p1 -i ../0001-treeview-Remove-some-dead-code.patch
    patch -p1 -i ../0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0002-treeview-Add-signal-rubber-banding-active-and-gtk_tr.patch
    patch -p1 -i ../0002-treeview-Fix-visual-glitch-on-row-inserted.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
    patch -p1 -i ../0004-treeview-Add-button-in-test-rubber-banding.patch
    patch -p1 -i ../0005-treeview-Add-skip_next_button_press.patch
    patch -p1 -i ../fix-708148.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install
    rm -f "$pkgdir/usr/bin/gtk-update-icon-cache"
}
