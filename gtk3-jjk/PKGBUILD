# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.10.2
pkgrel=3
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa gtk-doc)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool' '!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
        0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
        0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
        0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
        0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
        0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0001-Revert-Deprecate-and-ignore-gtk-menu-images-setting.patch
        0002-Revert-Deprecate-and-ignore-gtk-button-images-settin.patch
        0001-Fix-GtkToolButton-possibly-being-empty.patch
        0001-Revert-treeview-Store-editable-position-differently.patch
        0001-Fix-key-Page_Down-identified-as-Next.patch
        fix-708148.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('93af12d28e5f6ccc373ea59f31147e2884c9b3c15dc4841ce3b5cee45b13814c'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            '940925a5f1d17d3a94cc14d97b4b035f2020c2fbcd6bc80afd244f5f5ccfe94d'
            '0f762da916bd9480028df1260df8a127865cc61ee88ef35987a15818103a33e3'
            'eed27f3e1915dffd0b5ab9c016849beb6963d58575b16c643a89109a7ca4716b'
            '545f52973179f2fbe7dc7a61cee8df84256c4c74726e91b6a205ff95834eec69'
            '9d45bc62f62276f5e4060d314ce38765170b7f30538665898667f43e2b641578'
            'a7e5a413d606867c2b67f07653ed5e971d273a5b630cbb7798ba758772b6b76c'
            'f11754f8f812e895e14fff06d074973b7755034cb2a3c882ea176f8b2ab63e25'
            'dabbb3db4b0618ff7db1da585bb8e5786a859510a13887e97a30b35a97eae110'
            'd65411f209bf581c0c7de262f53621d8934d7a1603130ef6671a097b6b6676ff'
            '5bfa575d44e92ba5a5841152e93b06f6bfef9373122be9f08c8d49ed66c8c0d8'
            '72b95ee04f52c8bd9089d2910a7691a9dd517416cb1df8f60396b0b58efe5e53'
            'c76bc7cb66aadb6b5364737b360a1caf69c428d394c50ecbf8a40e308d9abc3f'
            'ff5aeeaad056b9aa30d7634886ce479e93f7ace83c6eead9206a1885729f2ed0'
            '178d547fbd1bb91b77a6b9bb9f6b79bcb3ea60cdc979c3b8fc25bf2230b3939c'
            '4702dd3032a094bec6805843a52073ddf9659ac6daaea65891f70cf643798919'
            '7ec1abd2ac10482093a1668521e9979f3777dea5bcbfa8f8d6fd332b0fae8727'
            '9bfdfe3b3015605a35001878c04d8228ed5f242544b24abb5cad6a4707b0d0c1'
            'b561dde328d8d36c993f25efbbe6067776f6594d1631274fbe004b4e8d67146c'
            'df86481bf44fa0baba896382aa6177983dc27c81bbdbdfabc858dce0766b29d7'
            '49d40ba555f8ea07ccbf7ef6eb8be4c08f5ddb5eabdd39d8fe6f24dcd128f54e'
            '0b0eb70ceffbc420ac65d5a0145220fb979caa57d60d02eee1622d9d109d7cf9'
            '8a8b500f6850466b646f788ee45756bbba21dea6f75babddca1d4b32ea1b0b03'
            'efe759bc260b5aeb9a22c97a653d6b2a2a491ede71bfbff15fa4779a979f38a1')
prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
    patch -p1 -i ../0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
    patch -p1 -i ../0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
    patch -p1 -i ../0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
    patch -p1 -i ../0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
    patch -p1 -i ../0001-Fix-GtkToolButton-possibly-being-empty.patch
    patch -p1 -i ../0001-Revert-treeview-Store-editable-position-differently.patch
    patch -p1 -i ../0001-Fix-key-Page_Down-identified-as-Next.patch
    patch -p1 -i ../fix-708148.patch

    patch -p1 -i ../0001-Revert-Deprecate-and-ignore-gtk-menu-images-setting.patch
    patch -p1 -i ../0002-Revert-Deprecate-and-ignore-gtk-button-images-settin.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/usr/share/gtk-3.0/settings.ini"
}
