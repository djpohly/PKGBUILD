# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.10.1
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa gtk-doc)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool' '!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-tooltip-Fix-possible-wrong-placement.patch
        0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
        0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
        0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
        0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
        0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
        0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0001-Revert-Deprecate-and-ignore-gtk-menu-images-setting.patch
        0002-Revert-Deprecate-and-ignore-gtk-button-images-settin.patch
        0001-gtkicontheme-Fallback-to-regular-icons-when-symbolic.patch
        0001-Fix-GtkToolButton-possibly-being-empty.patch
        fix-710073.patch
        fix-709967.patch
        fix-708148.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('c12e6897fb1ec8d8f1a6de6cd0ac1372fee6fd63ee3a5a63813dc5f3473e6ab8'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            '552217ebe5ffe32b2b9fa230423eba695c4db62f394ade87cebe415b610d809e'
            '09a22c5cf1c8983b9169a4abd8e8aeca28693d21471e9b09317cc2612ba185f3'
            '760c98cdb7831c48971fbc196666bb185e3f6aab1e5ad0d3f172fd77c2670601'
            'c9c9aaad8d071b387963bd6c8790737523475aeae863ed5a1bc7a2e343bcd273'
            '2fb0b0a60181dc97f1104a42380bcc12c1c40418036aede63a9ba74427d4d356'
            '719879821c616e859a0b420e1faa4701d78bf6314c3e5e48f8d14f1512d94ab4'
            'df1921cc52bebc24cbcc87fd33cf807d5744a36e6e3b089efcb45054b6e9be8a'
            'a426bda35ca16bf734eeeb6782032cabd06408ce5a77d4ca4ee2577954fd62b1'
            'd751a50d8172adb68181797d39abcf9fb79ecf34a6c74987de5b66c1e4a9d868'
            'e7f4fd835c1c6a8d63daf1f36b73504ea55200ba1432efaaee3784bf4f084d0a'
            '32952272d9c4917558415d9211405ad4fb299d85c82c4e11cd4b5723e6987859'
            '55b4254ec696ecc1ddb0d4649513bfa2496a3a01f111ef2d522899a7e4c28d7c'
            '5316650d21bc21a50cd8baaa7ad4194bf84837d6e75d3fbc81e502de5f4563b6'
            'f06b7414749c6bc3cf851da7481228ccfb1d8c6308298f115654c6744b949478'
            'a06254020b1657916238b98093074dc395787dd0f60db4e398989d1f37efccca'
            'a91f43869f86e38f52677a519024df90721c9ad5f69089767fde0c94be6d2504'
            '9bfdfe3b3015605a35001878c04d8228ed5f242544b24abb5cad6a4707b0d0c1'
            'b561dde328d8d36c993f25efbbe6067776f6594d1631274fbe004b4e8d67146c'
            '62157382639e0a80d3ab106c8aacaade3761753f06ad1db1718fe539a6a6e912'
            'df86481bf44fa0baba896382aa6177983dc27c81bbdbdfabc858dce0766b29d7'
            'e856d70078ee76778c24f22eabe31e209d6eb7762e0f1cd78ad7c42f3b606b5c'
            '5104ee652e5fade16448e4c319df2a328c977b60ab2cf355b4b9631eb5da18b2'
            '653e1c3a988b1ebd91ffbc760a99cad23f103ff03f89886dac02a7fbdc61c8a6')
prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
    patch -p1 -i ../0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
    patch -p1 -i ../0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-tooltip-Fix-possible-wrong-placement.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
    patch -p1 -i ../0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
    patch -p1 -i ../0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
    patch -p1 -i ../0001-Fix-GtkToolButton-possibly-being-empty.patch
    patch -p1 -i ../fix-710073.patch
    patch -p1 -i ../fix-709967.patch
    patch -p1 -i ../fix-708148.patch

    patch -p1 -i ../0001-Revert-Deprecate-and-ignore-gtk-menu-images-setting.patch
    patch -p1 -i ../0002-Revert-Deprecate-and-ignore-gtk-button-images-settin.patch
    patch -p1 -i ../0001-gtkicontheme-Fallback-to-regular-icons-when-symbolic.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/usr/share/gtk-3.0/settings.ini"
}
