# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.12.0
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa gtk-doc)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
        0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
        0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
        0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0001-Revert-treeview-Store-editable-position-differently.patch
        0002-treeview-Fix-visual-glitch-on-row-inserted.patch
        0001-cellarea-Fix-possibly-getting-invalid-inner_area.patch
        0001-treeview-Fix-possible-column-rendering-issue.patch
        0001-treeview-Remove-some-dead-code.patch
        0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
        0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
        fix-708148.patch
        0001-menu-Fix-possible-leak-on-destroy.patch
        0001-treemodelfilter-Fix-using-wrong-path-on-row-deleted.patch
        0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('eb69741cd4029b5a1ac9cf04d9de55dcf9e30777a63891750f5d20cc554b6e4b'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            '8120899a66c633f5aa1d897adf40139eaa6554dc58f2bda582424e8dddc2f724'
            'aa5917c4dd42d86ce078f12cb356d7417e134001b5dbc42e113ccf97443e230e'
            'a078367bdfb61c92225fdcc971af00fbea081e29e9070ff0572504b2cf55941a'
            'a612d2e7c863ddf2a3a36ace6803bfc1ea5303bd9e78540665e3335c221e969d'
            '437bb66b4406e661f9d1ed67bc3f8fb63dc48bd536ddff4c954158d58f001d90'
            '8e652d42a27bddaa575b294a00a35f12f762f3a7f996b71e88bcb6863ad63812'
            'f267421f5a88281c4aaceb7f2d32210af78af30321be83b0f4474e686f50a9ab'
            'f1ecebb4df0b5135d64accdcdb890d7130ab10d1118282fd2c00292aaef3aeed'
            '16a1fb557655dd83125d0bf45af1ae32df3dd18e829f165ce7c212e7fca72b4d'
            '7d5cfcfa90ade72db68b887e1514fe25399e43769a9a7a243ffdf7bdcc7aa3e8'
            '2ec172b4f7f81868c5b1ee6891b9869cb44d65ff30d3650b9a1ba8aac7824e1f'
            'd4ee2cd07a3822f1f10677641c3fa7bbc3e90b379fbf8657164b76229a40159c'
            'ed9b39613f9124ad2c2aa525d3f7f63ca988c1e92fa1915929683221eec7e961'
            'f192a61fae6f9579b24e9057bd111b3de61d9e05c675454700be00bc2d2462ee'
            '0391ca4e41917468b6642edd8710a77a3146d46fc00be85602431b810e375b29'
            'b8eb466c1b3313e099760c930870c987377e7ded41ecb2ef8de9dcc255a016fe'
            'b6b764db6bf5c89b8636bc491be11a13573ff5af6c12cd31906389694ac7d379'
            '4d85f2818bfb48d65a65c470dd86ec704a724982e048c90a22ad91ddd2ae8ca1'
            'adaaa8188edf53364f21006b352a72b1008d27cdaf1b8ac8ce3b70ed60ca9b54'
            'ab1164549c508f2b492966bacaeebc6302d64ecbe0c3884eb82048b1d292ce92'
            'ed8c7b271c2f4528dc81b9429a933dd0f058c91b6268a3c4930992a4686583cd'
            '8a8b500f6850466b646f788ee45756bbba21dea6f75babddca1d4b32ea1b0b03'
            'e3b82c6447fbd337356bc147ec64e8909ffc8fe11211180753ad74c2619f79d9'
            '75d75f8d86793f7f8c1c7b891165ed73ebb465e548b57612fb8ea2e414db781f'
            '3dbf42fe7cfaeb695507f3c7ba8531d7b6ef33eaf4b54249d99f31748c0d7d09'
            '01802065c6118b8612a2a1df2a257b96124261f0f118b896aa7cfe640c9330d7')

prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
    patch -p1 -i ../0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
    patch -p1 -i ../0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
    patch -p1 -i ../0001-Revert-treeview-Store-editable-position-differently.patch
    patch -p1 -i ../0002-treeview-Fix-visual-glitch-on-row-inserted.patch
    patch -p1 -i ../fix-708148.patch
    patch -p1 -i ../0001-cellarea-Fix-possibly-getting-invalid-inner_area.patch
    patch -p1 -i ../0001-treeview-Fix-possible-column-rendering-issue.patch
    patch -p1 -i ../0001-treeview-Remove-some-dead-code.patch
    patch -p1 -i ../0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
    patch -p1 -i ../0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
    patch -p1 -i ../0001-menu-Fix-possible-leak-on-destroy.patch
    patch -p1 -i ../0001-treemodelfilter-Fix-using-wrong-path-on-row-deleted.patch
    patch -p1 -i ../0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/usr/share/gtk-3.0/settings.ini"
}
