# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.10.3
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa gtk-doc)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
        0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
        0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
        0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
        0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0001-Fix-GtkToolButton-possibly-being-empty.patch
        0001-Revert-treeview-Store-editable-position-differently.patch
        0001-cellarea-Fix-possibly-getting-invalid-inner_area.patch
        0001-gtkpixelcache-Fix-possibly-ignoring-queued-redraw.patch
        0001-treeview-Fix-possible-column-rendering-issue.patch
        0001-treeview-Remove-some-dead-code.patch
        fix-708148.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('8fb41deb898ad00ee858d5c04e2f80d295fbd6a8e9688cd7c0ba56ab84c6b727'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            '4f867b6aa6c81867fc56c0c6f9061d336234e23fa5bd8830032a904b05c04a41'
            'd67a39fde275e6d8be0bc52588e3d5c2140fa11fe73022deaad5c97990689811'
            '73758c97f017322c643e11bbbf85bc3f71d04af8156948e1d59678da2c12cd2a'
            'd918c7e9e08850a2f99eaa53d2e8d873c06d455a686238d5fbc940b59ada48d9'
            '38a18c144f7a22760a630bac4a67126d8d2889b75e76e1d1ca776715773d6fff'
            'b1d306806db164bdeec1b01c5b8b296fbec3173dff3941b013db134bb933788e'
            '7488b2bf20579074c6a3f9ef8b58ebd5c45cf21b23813dbe909b59bd43d80dcc'
            '3efab352b399dc73a2a8bb61277fb3a721999de2b32ce73614b75c667d1501b0'
            '2618b494ead5ed327cca570ca512cf480287cdc6b463c96dc7a4a3305cd5a41f'
            '222518481e63281a07bc54fe2c16ff94ee18110b1e51571316dcf35fdab79485'
            '78b89fa87ce876eeb6b9b4fce4ca439936ff0591e154a5615afb68e508a79207'
            '281eb70fdcec59859cd195b5a2afbb0abff8d846c9e1ac59ef59fe8cb8863d38'
            '92c7a768f9a9e664128d4671f9c50c08a091ac1765ffeb4d02e8c38b8d52e810'
            '095a83ce851772c659735e31cf9deedd298614c3ab47d2a977c0d17933bea3d6'
            '8d3216f839ac58dfef075785da70f07958aa0cfe13a438ed5c12b83163c1d0dc'
            'd7e42b16bf116b1395591a83f8c61e9e4e01ed9f370965ca690ec11a5255c99d'
            'a52bdb3819256b062eb1e571267571268389efc3d53b4024db9dccdd407f4328'
            '6ace73fa96de82af6b2e752040bd886e821b33ecb151bbe6b7b51b8d8a106456'
            '15a26985ee733bb30ccbf4863e99539e4c6c6565f7bde2bf7a072f4bd1520a49'
            '590387fbd4701c511d2d91c1bbc0d18a03722be7602686da5bddd5293adcc57d'
            'c2ed59e8181ee3b8632e38e6517855a712818b0e6ee6868bafd0d1288e87c45d'
            '8a8b500f6850466b646f788ee45756bbba21dea6f75babddca1d4b32ea1b0b03'
            'bafcc5c42cad39566edf54bf30ddaa9fc37f47f326a7d0ae62b58206d84f43dc')
prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
    patch -p1 -i ../0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
    patch -p1 -i ../0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
    patch -p1 -i ../0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
    patch -p1 -i ../0001-Fix-GtkToolButton-possibly-being-empty.patch
    patch -p1 -i ../0001-Revert-treeview-Store-editable-position-differently.patch
    patch -p1 -i ../fix-708148.patch
    patch -p1 -i ../0001-cellarea-Fix-possibly-getting-invalid-inner_area.patch
    patch -p1 -i ../0001-gtkpixelcache-Fix-possibly-ignoring-queued-redraw.patch
    patch -p1 -i ../0001-treeview-Fix-possible-column-rendering-issue.patch
    patch -p1 -i ../0001-treeview-Remove-some-dead-code.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/usr/share/gtk-3.0/settings.ini"
}
