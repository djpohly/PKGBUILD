# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.14.6
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk
         adwaita-icon-theme)
makedepends=(gobject-introspection mesa gtk-doc)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-Add-GTK_IS_JJK.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-menu-Fix-possible-leak-on-destroy.patch
        0001-revert-changes-to-GtkMessageDialog.patch
        0001-Revert-treeview-Store-editable-position-differently.patch
        0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
        0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-treeview-Add-signal-test-rubber-banding.patch
        0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
        0001-treeview-Better-resizing-with-expand-columns.patch
        0001-treeview-Fix-only-headers-being-refreshed-on-validat.patch
        0001-treeview-Fix-possible-column-rendering-issue.patch
        0001-treeview-Fix-prelight-re-gesture-refactoring.patch
        0001-treeview-Remove-some-dead-code.patch
        0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0002-treeview-Add-signal-rubber-banding-active-and-gtk_tr.patch
        0002-treeview-Fix-visual-glitch-on-row-inserted.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0004-treeview-Add-button-in-test-rubber-banding.patch
        0005-treeview-Add-skip_next_button_press.patch
        fix-708148.patch)
sha256sums=('cfc424e6e10ffeb34a33762aeb77905c3ed938f0b4006ddb7e880aad234ef119'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            '853d634718f1a3a8bb70c2191067fbec9e55763c482946ba2594a9062a81db98'
            '29d93cc8fb7831d32e626dd85f07eb38e9fcd300e6580a38e0e6d1f5f42d64a2'
            'a59b332c42c52f8e0c47d3b169b591d97da72d510fa5012fedf7db9d5d28f051'
            '4a01795128acdecce9fae9a899203b93284951a8cf902981edeae3fb183a6ec4'
            'a620abc60dad8f3e0985c0c7f9abe0b2da1f93d7b8f81bea243ba4d6be494d2e'
            'edaf45dcdc9dd47eb1d5afe3a6edd90bc5d05ad2bfa6d747097ec51f18462965'
            '62fbc791460910798750c39f4e95fdebd1a773d702f2d5ee71967fef00f47323'
            '148880a7a174fcf5daba19e8150d7902eb45863ef397c51ea8c7f3d945392700'
            '69b55e173acda41a87ee65d0c306c39f820faff86e9def20107e21a7097ec432'
            '95528010e2775517ace957a6ef6b59c036ceb847be69b3c3b2dcbd441916034f'
            'a74e1b42ef086d738dcf72399910efc467cd8b83aea0526c81290416aeb07a2f'
            'ba2e2a92f35e8ee8ae1aa63ae7a5e8f67aa0be718fb6a19417c9f5c5539a927b'
            '375c8acdaf1c46c92f30e197b6e8e03135c4f19d26b6730b546dd4f3225246b5'
            '62b1923b43c76cb61bf9837bc700f67ddb4257661d83232beea4764d72409166'
            '0d3ad2d4e6f94bdd1bfab200c3e07d5e37612ba198f5d6b9fb68072ac4fbbd53'
            '533acec45dcd618ff1db25790697e98f57f0192a2b67c723818acf3fa9be5f2b'
            '9627df34dda7f10800acf72b9f6490826a8bf13a5606c0bb5ee699a96144dc94'
            '259aacd3cda41ff51ff40666123895d1a21a01660945000f201b2e8ee9f00eee'
            '2a58b6f6167de22e2087002f5ecd6e03881c6a29d876b2940a805c361ab2d997'
            '42c878fd96ccb1c3e06c2c97b7b5a02c868965df09764c599d46710a35253462'
            '35ac4f7734c1652c78edaa4f9faa356673188911dc59ca5cf04d0cc1c47e6d49'
            '7e6fca7e689783037c73a87708355081de85e6a3a321d02b8c9ed72d82997268'
            '965fc851a26ddea6db1db45d6b2b158e2fc53005f21dbf49ec848053cf75d031'
            '385324e86dae3bda870f5dc075a9a96f45d1865380596e48d995f6e1ca7a344e'
            'e393abb98a12ed080a88bbee56ecb23fd0c3ac6a61fb4c5a22c0e7f50b92344f'
            '0144ad09257d75dc60ecfc6c024e18b2dee016c03a9c5c9fec184b9f4865dc95'
            'e9c8ccf6732e31bb6e5103d716c82f68ef91d6eb3090fde2b9d0b251d7e49e10'
            '8a8b500f6850466b646f788ee45756bbba21dea6f75babddca1d4b32ea1b0b03')

prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-menu-Fix-possible-leak-on-destroy.patch
    patch -p1 -i ../0001-revert-changes-to-GtkMessageDialog.patch
    patch -p1 -i ../0001-Revert-treeview-Store-editable-position-differently.patch
    patch -p1 -i ../0001-Some-fixes-in-gtk_tree_view_is_blank_at_pos.patch
    patch -p1 -i ../0001-tooltip-Fix-possibly-briefly-appearing-at-0x0.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-treeview-Add-signal-test-rubber-banding.patch
    patch -p1 -i ../0001-treeview-Avoid-calling-do_validate_rows-if-not-neede.patch
    patch -p1 -i ../0001-treeview-Better-resizing-with-expand-columns.patch
    patch -p1 -i ../0001-treeview-Fix-only-headers-being-refreshed-on-validat.patch
    patch -p1 -i ../0001-treeview-Fix-possible-column-rendering-issue.patch
    patch -p1 -i ../0001-treeview-Fix-prelight-re-gesture-refactoring.patch
    patch -p1 -i ../0001-treeview-Remove-some-dead-code.patch
    patch -p1 -i ../0002-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0002-menu-Add-helper-gtk_menu_popup_and_destroy.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0002-treeview-Add-signal-rubber-banding-active-and-gtk_tr.patch
    patch -p1 -i ../0002-treeview-Fix-visual-glitch-on-row-inserted.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
    patch -p1 -i ../0004-treeview-Add-button-in-test-rubber-banding.patch
    patch -p1 -i ../0005-treeview-Add-skip_next_button_press.patch
    patch -p1 -i ../fix-708148.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install
}
