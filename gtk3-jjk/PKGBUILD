# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gtk3
pkgname=$_pkgname-jjk
pkgver=3.10.0
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libxcursor libxinerama libxrandr
         libxi libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=(gobject-introspection mesa)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool' '!strip' 'debug')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
        0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
        0001-tooltip-Fix-possible-wrong-placement.patch
        0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
        0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
        0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
        0001-treeview-Add-gtk_tree_view_set_focused_row.patch
        0001-Allow-to-disable-full-row-highlight-on-selection.patch
        0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
        0003-treeview-Add-GtkTreeBoxable.patch
        0001-Add-gtk_tree_selection_invert_range.patch
        0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
        0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
        0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
        0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
        0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch
        0001-Revert-Deprecate-and-ignore-gtk-menu-images-setting.patch
        0002-Revert-Deprecate-and-ignore-gtk-button-images-settin.patch
        0001-gtkicontheme-Fallback-to-regular-icons-when-symbolic.patch
        0001-Add-GTK_IS_JJK.patch)
sha256sums=('6559feb360cd935d341cd7a0b69a72f8f4346ed6ee9b7c4040c02b73b75c53fe'
            'de961084b35507da5415640db733457161e1672ee54639828dfa399ea3d220b1'
            '552217ebe5ffe32b2b9fa230423eba695c4db62f394ade87cebe415b610d809e'
            'cf2adbee473912f40d14e32cf0a96a24525b8d1885808758a8bb0ab8edc9047e'
            '30220311a2e238ad107cd0e072969048df182e7a6a7935f68ac848b3831910eb'
            '548bddf555e1f81fd3f3e6bc4f6829e22b7dceb64b7500b1116a1135baee7295'
            '27fe3a7467a7bb90f9ccc0a661ce63364528eaac41d6d06cf34ecdb4ae1d5b89'
            '984e0c9a7e04756d7e03cfacaf48e45d757fbc9df322be3b116351c19af64150'
            '43f8e883b5701915135cbaf096aaddbbe13c9a2c5126c0cbfe10af77d987567d'
            'b7f8a704f59c69c38472b6d797aa7d298c62e9a9c8a828335ff58f6ffb0a961f'
            '727847afac39bf8447870977154b0ed143d51c78b0ac60a0bb99fd01259d41a2'
            '424ede11b86bd6a7c5d1dd65f2951f08f0c54e372eb5958e6abda5644141291d'
            'f11d9f2d2104f4e00bb0dd3c95cd52955415ff77fd5069ea242a657112939cba'
            '5bda2d381595c0b8cfdf42d329b8951583ed5d96e2bd73604862e0092b48033d'
            '84f3d6bc0fbfed60bf0f0df7361fb8c8933cd7cd191d7c9ded742f97eb210d41'
            '2376f7608afc9424972726a5affc0472d436ea737cf1611509baea0965a92ee4'
            '245bc80171d40bcbdeffd3cde167515d4ca1079341066448cbefa8b6c990655c'
            'b302a82cff5eb2048fdd1615b6f1598d2dfac4eddc5f02e64129aac722dc93ef'
            '3990907f7eb9cfe2dcb8eafede32229c0b036c944bb13f165b54cec488afcc3f'
            'c3cb4b9e9b2efeab5646d592f112bf6c8964e5ffa50c7ff925a31126fbb2abed'
            '69d9721611cbb8a848308301e726b7c491dd4c1226f3c7ad9f10935ab884be0d'
            'f64e9be57b52dd0870594d50098396ebc8e4caffe7fa187c538ff8eafb39e87e')
prepare() {
    cd "gtk+-$pkgver"

    patch -p1 -i ../0001-Add-GTK_IS_JJK.patch
    patch -p1 -i ../0001-gtkcellareabox-Fix-renderer-not-always-drawn-when-vi.patch
    patch -p1 -i ../0001-Add-gtk_tree_view_is_blank_at_pos_full.patch
    patch -p1 -i ../0001-treestore-Fix-segfault-when-reordering-non-existing-.patch
    patch -p1 -i ../0001-tooltip-Fix-possible-wrong-placement.patch
    patch -p1 -i ../0001-cellrenderertext-Fix-possible-overwriting-of-markup.patch
    patch -p1 -i ../0001-Add-class-focused-row-to-the-focused-row-regardless-.patch
    patch -p1 -i ../0001-treeview-Add-gtk_tree_view_set_focused_row.patch
    patch -p1 -i ../0001-Allow-to-disable-full-row-highlight-on-selection.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_set_row_class_column.patch
    patch -p1 -i ../0003-treeview-Add-GtkTreeBoxable.patch
    patch -p1 -i ../0001-Add-gtk_tree_selection_invert_range.patch
    patch -p1 -i ../0001-GtkTreeView-Fix-issues-when-removing-a-column.patch
    patch -p1 -i ../0001-Fix-scrolling-menu-not-working-properly-Bug-701077.patch
    patch -p1 -i ../0001-treeview-is_rubber_banding_active-only-based-on-stat.patch
    patch -p1 -i ../0002-treeview-Add-gtk_tree_view_start_rubber_banding.patch
    patch -p1 -i ../0003-treeview-Change-how-toggle-in-handled-on-rubber-band.patch

    patch -p1 -i ../0001-Revert-Deprecate-and-ignore-gtk-menu-images-setting.patch
    patch -p1 -i ../0002-Revert-Deprecate-and-ignore-gtk-button-images-settin.patch
    patch -p1 -i ../0001-gtkicontheme-Fallback-to-regular-icons-when-symbolic.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --disable-cups \
        --disable-colord \
        --enable-x11-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/usr/share/gtk-3.0/settings.ini"
}
