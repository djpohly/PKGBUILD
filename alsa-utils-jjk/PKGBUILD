# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgname=alsa-utils
pkgname=$_pkgname-jjk
pkgver=1.1.4
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'pciutils' 'ncurses' 'psmisc' 'libsamplerate')
makedepends=('xmlto' 'docbook-xsl')
provides=("$_pkgname")
conflicts=("$_pkgname")
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$_pkgname-${pkgver}.tar.bz2)
sha256sums=('a7831044de92c5bf33bf3365a3f36e49397f4191e934df460ae1ca15138c9d9d')

build() {
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --disable-bat \
    --with-systemdsystemunitdir=no
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # remove udev rules (only udev/rules.d under lib)
  rm -rf "$pkgdir"/lib

  # dir where to save ALSA state
  install -d "${pkgdir}"/var/lib/alsa
}
