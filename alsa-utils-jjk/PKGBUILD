# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgname=alsa-utils
pkgname=$_pkgname-jjk
pkgver=1.1.1
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'pciutils' 'ncurses' 'psmisc' 'libsamplerate')
makedepends=('xmlto' 'docbook-xsl')
provides=("$_pkgname")
conflicts=("$_pkgname")
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$_pkgname-${pkgver}.tar.bz2
        90alsa)
sha256sums=('89757c9abaf420831b088fce354d492acc170bd02bb50eb7392c175f594b8041'
            'df0170be687bd474e0677ab97b94b86879fd1a86e5a714fa8b46b3af785f2496')

build() {
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --disable-bat \
    --with-systemdsystemunitdir=no
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m755 ../90alsa "${pkgdir}"/etc/pm/sleep.d/90alsa

  # remove udev rules (only udev/rules.d under lib)
  rm -rf "$pkgdir"/lib

  # dir where to save ALSA state
  install -d "${pkgdir}"/var/lib/alsa
}
