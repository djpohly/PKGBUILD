# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_pkgname=pinentry
pkgname=$_pkgname-jjk
pkgver=1.1.0
pkgrel=1
pkgdesc='Collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol'
url='https://gnupg.org/related_software/pinentry/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('ncurses' 'libcap' 'libassuan')
makedepends=('gtk2')
optdepends=('gtk2: gtk2 backend')
provides=($_pkgname)
conflicts=($_pkgname)
source=("https://gnupg.org/ftp/gcrypt/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"{,.sig})
sha1sums=('693bdf9f48dfb3e040d92f50b1bb464e268b9fb0'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--enable-pinentry-tty \
		--enable-pinentry-curses \
		--enable-fallback-curses \
		--enable-pinentry-gtk2

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
