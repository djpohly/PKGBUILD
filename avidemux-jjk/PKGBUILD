# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=avidemux
pkgname=('avidemux-cli-jjk' 'avidemux-gtk-jjk')
pkgver=2.5.6
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
url="http://fixounet.free.fr/avidemux/"
makedepends=('cmake' 'libxslt' 'gtk2' 'libvorbis' 'sdl' 'libxv'
             'alsa-lib' 'lame' 'xvidcore' 'faad2' 'libx264' 'libsamplerate'
             'yasm' 'mesa' 'libvpx' 'libpulse' 'libva')
options=('!makeflags' '!emptydirs')
source=(http://downloads.sourceforge.net/avidemux/avidemux_$pkgver.tar.gz
        mangled-variables-used-attribute.patch
        hack.patch
        fix.patch
        buildfix.patch)
sha1sums=('47205c236bf6a4435b9d4dd944493c7b7e2752f5'
          'da66b69593da62373f2309f88a284145fbfd99ac'
          'c9666e50587aaf60f53da9bcf7bda523269242ca'
          'ba14a6e94ada00dd814ceaa08bbcf674cf8ca26e'
          '8b4b45627a8ed4409e32ac2ece361a0cf935e9d4')

prepare() {
  cd ${pkgbase}_$pkgver

  # Fix build on i686 when using GCC 5.1
  # https://bugs.archlinux.org/task/45361
  patch -Np1 -i ../mangled-variables-used-attribute.patch

  # Fix build with gcc 6.1 and libvpx 1.6.0
  patch -Np1 -i ../buildfix.patch

  patch -p0 -i ../hack.patch
  patch -p0 -i ../fix.patch
}

build() {
  cd ${pkgbase}_$pkgver
  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_SKIP_RPATH=ON -D GTK=1 ..
  make

# plugin build expects libraries to be already installed; we fake a prefix
# in build/ by symlinking all libraries to build/lib/
  cd lib
  find ../avidemux -name '*.so*' | xargs ln -sft .
  cd ../../plugins
  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D AVIDEMUX_SOURCE_DIR="$srcdir/avidemux_$pkgver" \
    -D AVIDEMUX_CORECONFIG_DIR="$srcdir/avidemux_$pkgver/build/config" \
    -D AVIDEMUX_INSTALL_PREFIX="$srcdir/avidemux_$pkgver/build" \
    -D CMAKE_BUILD_TYPE=Release -D CMAKE_SKIP_RPATH=ON ..
  make
}

package_avidemux-cli-jjk() {
  pkgdesc="A graphical tool to edit video (filter/re-encode/split)"
  depends=('libxml2' 'sdl' 'fontconfig' 'libvpx' 'libva')
  optdepends=('lame: for the corresponding audio encoder plugin'
              'faad2: for the corresponding audio decoder plugin'
              'libpulse: for the corresponding audio device plugin'
              'sdl: for the corresponding audio device plugin'
              'libx264: for the corresponding video encoder plugin'
              'xvidcore: for the corresponding video encoder plugin')
  provides=('avidemux' "avidemux-cli=$pkgver")
  conflicts=('avidemux' 'avidemux-cli')
  replaces=('avidemux')

  cd ${pkgbase}_$pkgver/build
  make DESTDIR="$pkgdir" install

  cd ../plugins/build
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/share/ADM_addons/avsfilter/{avsload.exe,pipe_source.dll}
  rm "$pkgdir"/usr/bin/avidemux2_gtk
  rm "$pkgdir"/usr/lib/*{_gtk,Gtk}.so
  rm "$pkgdir"/usr/lib/ADM_plugins/videoEncoder/x264/*Gtk.so
  rm "$pkgdir"/usr/lib/ADM_plugins/videoEncoder/xvid/*Gtk.so
  rm "$pkgdir"/usr/lib/ADM_plugins/videoFilter/*_gtk.so

  ln -s /usr/lib/ADM_plugins/videoEncoder/libADM_vidEnc_xvid.so "$pkgdir/usr/lib/libADM_vidEnc_xvid.so"
  ln -s /usr/lib/ADM_plugins/videoEncoder/libADM_vidEnc_x264.so "$pkgdir/usr/lib/libADM_vidEnc_x264.so"

  install -D -m644 ../../avidemux_icon.png "$pkgdir/usr/share/pixmaps/avidemux.png"
  install -D -m644 ../../man/avidemux.1 "$pkgdir/usr/share/man/man1/avidemux.1"
}

package_avidemux-gtk-jjk() {
  pkgdesc="A graphical tool to edit video (filter/re-encode/split) - GTK GUI"
  depends=("avidemux-cli=$pkgver" 'gtk2' 'libxv' 'desktop-file-utils')
  provides=('avidemux-gtk')
  conflicts=('avidemux-gtk')

  cd ${pkgbase}_$pkgver
  install -D -m755 build/avidemux/avidemux2_gtk "$pkgdir/usr/bin/avidemux2_gtk"

  install -d "$pkgdir"/usr/lib/ADM_plugins/videoEncoder/{x264,xvid}
  install -d "$pkgdir"/usr/lib/ADM_plugins/videoFilter

  install -D -m755 build/lib/*{_gtk,Gtk}.so "$pkgdir/usr/lib"
  install -D -m755 plugins/build/ADM_videoEncoder/ADM_vidEnc_x264/gtk/*Gtk.so "$pkgdir/usr/lib/ADM_plugins/videoEncoder/x264"
  install -D -m755 plugins/build/ADM_videoEncoder/ADM_vidEnc_xvid/gtk/*Gtk.so "$pkgdir/usr/lib/ADM_plugins/videoEncoder/xvid"
  install -D -m755 plugins/build/ADM_videoFilters/*/*_gtk.so "$pkgdir/usr/lib/ADM_plugins/videoFilter"

  install -D -m644 avidemux2-gtk.desktop "$pkgdir/usr/share/applications/avidemux-gtk.desktop"
  sed -i "s|\[\$e\]||" "$pkgdir/usr/share/applications/avidemux-gtk.desktop"
}
