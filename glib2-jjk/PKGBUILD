# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgname=glib2
pkgname=$_pkgname-jjk
pkgver=2.48.0
pkgrel=2
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=('LGPL')
depends=('pcre' 'libffi')
makedepends=('pkg-config' 'python' 'libxslt' 'docbook-xml')
optdepends=('python2: for gdbus-codegen')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!emptydirs' '!strip' 'debug')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz
        glib-compile-schemas.hook
        gio-querymodules.hook
        revert-warn-glib-compile-schemas.patch
        0001-GRWLock-Tweak-doc-to-make-things-a-bit-clearer.patch
        0002-GRWLock-Tweak-re-blocking-on-g_rw_lock_reader_lock.patch
        0003-GRWLock-add-critical-warnings-for-EDEADLK.patch)
sha256sums=('744be6931ca914c68af98dc38ff6b0cf8381d65e335060faddfbf04c17147c34'
            'e1123a5d85d2445faac33f6dae1085fdd620d83279a4e130a83fe38db52b62b3'
            '5ba204a2686304b1454d401a39a9d27d09dd25e4529664e3fd565be3d439f8b6'
            '049240975cd2f1c88fbe7deb28af14d4ec7d2640495f7ca8980d873bb710cc97'
            '34663c6c16ee4d7b9aa092223da315dab679e0ccabe131bb83bda63d1fdbdbf7'
            'a628db5b48a8c7c1d00550026dfe5a348837b3c50873ece878b017270b62f3f6'
            '1639b4626796e3dadcf9851d09dccb3fe5c6c819dedc7b37049af1ee305aad5f')

prepare() {
  cd "glib-$pkgver"

  patch -Rp1 -i "$srcdir/revert-warn-glib-compile-schemas.patch"
  patch -p1 -i ../0001-GRWLock-Tweak-doc-to-make-things-a-bit-clearer.patch
  patch -p1 -i ../0002-GRWLock-Tweak-re-blocking-on-g_rw_lock_reader_lock.patch
  patch -p1 -i ../0003-GRWLock-add-critical-warnings-for-EDEADLK.patch
}

build() {
  cd "glib-$pkgver"

  ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package() {
  cd glib-$pkgver

  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gdbus-codegen

  # install hooks
  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 "$srcdir"/{glib-compile-schemas,gio-querymodules}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
