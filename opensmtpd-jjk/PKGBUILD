_pkgname=opensmtpd
pkgname=$_pkgname-jjk
pkgver=5.9.2p1
pkgrel=1
pkgdesc='Free implementation of the server-side SMTP protocol'
arch=('i686' 'x86_64')
url='http://www.opensmtpd.org/'
license=('custom')
depends=('libasr' 'libevent' 'openssl')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
backup=('etc/smtpd/smtpd.conf')
options=('emptydirs')
install=opensmtpd.install
source=("http://www.opensmtpd.org/archives/$_pkgname-$pkgver.tar.gz")
sha256sums=('3522f273c1630c781facdb2b921228e338ed4e651909316735df775d6a70a71d')

build() {
  cd "$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --without-auth-pam \
    --with-user-smtpd=smtpd \
    --with-user-queue=smtpq \
    --with-group-queue=smtpq \
    --with-path-socket=/run \
    --with-path-CAfile=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -dm711 "$pkgdir/var/spool/smtpd"
}
