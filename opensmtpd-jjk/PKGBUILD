_pkgname=opensmtpd
pkgname=$_pkgname-jjk
pkgver=6.0.3p1
pkgrel=1
pkgdesc='Free implementation of the server-side SMTP protocol'
arch=('i686' 'x86_64')
url='http://www.opensmtpd.org/'
license=('custom')
depends=('libasr' 'libevent' 'openssl-1.0')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
backup=('etc/smtpd/smtpd.conf')
options=('emptydirs')
install=opensmtpd.install
source=("https://www.opensmtpd.org/archives/$_pkgname-$pkgver.tar.gz")
sha256sums=('291881862888655565e8bbe3cfb743310f5dc0edb6fd28a889a9a547ad767a81')

build() {
  cd "$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --with-libssl=/usr/lib/openssl-1.0 --with-cflags=-I/usr/include/openssl-1.0 \
    --without-auth-pam \
    --with-user-smtpd=smtpd \
    --with-user-queue=smtpq \
    --with-group-queue=smtpq \
    --with-path-socket=/run \
    --with-path-CAfile=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -dm711 "$pkgdir/var/spool/smtpd"

  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/sendmail"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/mailq"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/newaliases"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/makemap"
}
