# Maintainer: Jens Staal <staal1978@gmail.com>

_pkgname=busybox
pkgname=$_pkgname-musl-jjk
pkgver=1.26.2
pkgrel=1
pkgdesc="Combines common UNIX utilities into a single small executable, statically linked with musl libc"
arch=('i686' 'x86_64')
url="http://wiki.musl-libc.org/wiki/Building_Busybox"
license=('GPL')
provides=("$_pkgname" "mdev")
conflicts=("$_pkgname")
makedepends=('musl' 'kernel-headers-musl')
source=("https://www.busybox.net/downloads/$_pkgname-$pkgver.tar.bz2"
        "config" "busybox.mkinitcpio")
md5sums=('bb59d25ee2643db20f212eec539429f1'
         'f226136c9c30d8527de47c6e9caf0e07'
         '4428b5e6971ffc547ad8bd9da51461b0')

build() {
  cd "$_pkgname-$pkgver"

  cp ../config .config
  sed '1,1i#include <sys/resource.h>' -i include/libbb.h

  make CC=musl-gcc CFLAGS='-Os -static'
  cp .config ../config.new
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm755 busybox "$pkgdir/usr/bin/busybox"
  ln -s busybox "$pkgdir/usr/bin/mdev"
  install -Dm644 "$srcdir/busybox.mkinitcpio" "$pkgdir/usr/lib/initcpio/install/busybox"
}
