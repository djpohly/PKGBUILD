# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

_pkgname=ffmpeg
pkgname=$_pkgname-jjk
pkgver=2.1.1
pkgrel=2
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('i686' 'x86_64')
url="http://ffmpeg.org/"
license=('GPL')
depends=(
      'alsa-lib' 'bzip2' 'fontconfig' 'lame'
      'libpulse' 'libva' 'libvorbis' 'libvpx'
      'openjpeg' 'rtmpdump' 'sdl'
      'v4l-utils' 'x264' 'xvidcore' 'zlib'
      )
makedepends=('yasm')
provides=($_pkgname)
conflicts=($_pkgname)
source=(http://ffmpeg.org/releases/$_pkgname-$pkgver.tar.bz2
       ffmpeg-2.1.1-freetype2.patch)
sha256sums=('bef95e5b9aac45adec7ef434537504a63b4bbc22a8ce6a96a68e19c5efbadae2'
            '604739a31c42de7dbd3362f4c721192c478279d7258ac795cc68126562e50337')

prepare() {
  cd $_pkgname-$pkgver
  patch -p1 -i ../ffmpeg-2.1.1-freetype2.patch
}

build() {
  cd $_pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --enable-avresample \
    --enable-fontconfig \
    --enable-gpl \
    --enable-libfreetype \
    --enable-libmp3lame \
    --enable-libopenjpeg \
    --enable-libpulse \
    --enable-librtmp \
    --enable-libv4l2 \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libxvid \
    --enable-postproc \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-swresample \
    --enable-version3 \
    --enable-x11grab


  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-man
  install -D -m755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart"
}

# vim:set ts=2 sw=2 et:
