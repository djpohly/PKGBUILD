_pkgname=lxpolkit
pkgname=$_pkgname-jjk
pkgver=0.5.2
pkgrel=2
pkgdesc='Simple polkit authentication agent for LXDE'
arch=('i686' 'x86_64')
url="http://lxde.org/"
license=('GPL')
depends=('gtk2' 'polkit')
makedepends=('intltool' 'dbus-glib' 'libunique')
provides=($_pkgname)
replaces=($_pkgname)
conflicts=($_pkgname 'lxsession')
source=(http://downloads.sourceforge.net/lxde/lxsession-$pkgver.tar.xz)
md5sums=('2957acccbf9ce37ef8205ec5424f8047')

build() {
    cd "lxsession-$pkgver"
    ./configure --sysconfdir=/etc --prefix=/usr
    make
}

package() {
    cd "lxsession-$pkgver"
    make DESTDIR="$srcdir/pkg" install

    install -m755 -d "$pkgdir/usr/bin"
    mv "$srcdir/pkg/usr/bin/lxpolkit" "$pkgdir/usr/bin/"
    install -m755 -d "$pkgdir/usr/share/lxsession/ui"
    mv "$srcdir/pkg/usr/share/lxsession/ui/lxpolkit.ui" \
        "$pkgdir/usr/share/lxsession/ui/"
#    mv "$srcdir/pkg/usr/share/locale" "$pkgdir/usr/share"
    install -m755 -d "$pkgdir/etc/xdg/autostart"
    mv "$srcdir/pkg/etc/xdg/autostart/lxpolkit.desktop" \
        "$pkgdir/etc/xdg/autostart/"
    sed -i 's/^Hidden=.*$//' "$pkgdir/etc/xdg/autostart/lxpolkit.desktop"
}
