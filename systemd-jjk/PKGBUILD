# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

pkgbase=systemd
pkgname=systemd-jjk
pkgver=208
pkgrel=5
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
makedepends=('acl' 'cryptsetup' 'libdbus' 'docbook-xsl' 'gobject-introspection' 'gperf'
             'gtk-doc' 'intltool' 'kmod' 'libcap' 'libgcrypt' 'libxslt'
             'linux-api-headers' 'pam' 'python' 'quota-tools' 'xz')
options=('!libtool')
source=("http://www.freedesktop.org/software/$pkgbase/$pkgbase-$pkgver.tar.xz"
        'initcpio-hook-udev'
        'initcpio-install-udev'
        'initcpio-install-systemd'
        '0001-swap-fix-swap-behaviour-with-symlinks-2.patch'
        '0002-Make-swap-unit-follow-device-node-when-swap-is-off.patch'
        '0001-Resolve-dev-console-to-the-active-tty-instead-of-jus.patch'
        '0002-Only-disable-output-on-console-during-boot-if-needed.patch'
        '0003-Fix-possible-lack-of-status-messages-on-shutdown-reb.patch'
        '0001-mount-check-for-NULL-before-reading-pm-what.patch'
        '0001-shared-util-fix-off-by-one-error-in-tag_to_udev_node.patch'
        '0001-login-Don-t-stop-a-running-user-manager-from-garbage.patch'
        '0001-fstab-generator-When-parsing-the-root-cmdline-option.patch'
        '0002-fstab-generator-Generate-explicit-dependencies-on-sy.patch'
        '0003-gpt-auto-generator-Generate-explicit-dependencies-on.patch'
        '0004-Remove-FsckPassNo-from-systemd-fsck-root.service.patch'
        '0005-mount-service-drop-FsckPassNo-support.patch'
        '0006-efi-boot-generator-hookup-to-fsck.patch'
        '0007-fsck-root-only-run-when-requested-in-fstab.patch'
        '0001-systemd-order-remote-mounts-from-mountinfo-before-re.patch'
        '0001-acpi-fpdt-break-on-zero-or-negative-length-read.patch'
        '0001-cryptsetup-generator-auto-add-deps-for-device-as-pas.patch'
        '0001-do-not-accept-garbage-from-acpi-firmware-performance.patch'
        '0001-tmpfiles.d-include-setgid-perms-for-run-log-journal.patch'
        )
md5sums=('df64550d92afbffb4f67a434193ee165'
         '2de72238ed5c0df62a7c3b6bdaf8cb7c'
         'd83d45e67cd75cdbafb81c96a7485319'
         '9027b31a875e74a45623954b3b23d09f'
         '412f00062c58cc6875b567d95b10ed55'
         '2b0fda52012f1dde65cd4602300b857b'
         '0ed64035ca8d2cd9d9f566aaf3b3b75c'
         '94493246d9c415f5ab51911df94725e4'
         '418ce33eac6e4d4c75fab6d22599e02b'
         'a693bef63548163ffc165f4c4801ebf7'
         'ccafe716d87df9c42af0d1960b5a4105'
         '441e3d464ee6af5fe4af6a8bc10d7980'
         '718d841203cf2ea9e24a7d0f1d19d48b'
         '623c77bad0d2968e44963d72924825f1'
         'e52fc8368853c7800ab03ab8868cfd41'
         '2096f33bd36dfa0a7f0431d0a429787a'
         'd2481a6ea199b581e243a950125b0ca6'
         'c2aee634a3a6c50778968f0d5c756f40'
         'ef8b8212d504bb73c10bf4e85f0703b2'
         '8f1182afa1156f0076a912b23e761e02'
         '078f10d6fc315b329844cd20fa742eee'
         'f3e4e5b840cace769556e802466574da'
         '707d64bad7461c04d4cfce21bfddf712'
         '13232b7f28100e40990dde1c9e411596')

prepare() {
  cd "$pkgbase-$pkgver"

  # so swap can be stopped properly (e.g. on reboot)
  patch -Np1 <"$srcdir/0001-swap-fix-swap-behaviour-with-symlinks-2.patch"

  # makes sure starting always work even in odd cases
  patch -Np1 <"$srcdir/0002-Make-swap-unit-follow-device-node-when-swap-is-off.patch"

  patch -Np1 <"$srcdir"/0001-Resolve-dev-console-to-the-active-tty-instead-of-jus.patch
  patch -Np1 <"$srcdir"/0002-Only-disable-output-on-console-during-boot-if-needed.patch
  patch -Np1 <"$srcdir"/0003-Fix-possible-lack-of-status-messages-on-shutdown-reb.patch

  patch -Np1 <"$srcdir"/0001-mount-check-for-NULL-before-reading-pm-what.patch
  patch -Np1 <"$srcdir"/0001-shared-util-fix-off-by-one-error-in-tag_to_udev_node.patch

  # Fix lingering user managers
  patch -Np1 <"$srcdir"/0001-login-Don-t-stop-a-running-user-manager-from-garbage.patch
  # Backport changes in fstab passno handling
  # Basically, we only need 0001 and 0007, but 0007 is based on earlier patches,
  # and it doesn't hurt to backport them all.
  patch -Np1 <"$srcdir"/0001-fstab-generator-When-parsing-the-root-cmdline-option.patch
  patch -Np1 <"$srcdir"/0002-fstab-generator-Generate-explicit-dependencies-on-sy.patch
  patch -Np1 <"$srcdir"/0003-gpt-auto-generator-Generate-explicit-dependencies-on.patch
  patch -Np1 <"$srcdir"/0004-Remove-FsckPassNo-from-systemd-fsck-root.service.patch
  patch -Np1 <"$srcdir"/0005-mount-service-drop-FsckPassNo-support.patch
  patch -Np1 <"$srcdir"/0006-efi-boot-generator-hookup-to-fsck.patch
  patch -Np1 <"$srcdir"/0007-fsck-root-only-run-when-requested-in-fstab.patch
  #
  patch -Np1 <"$srcdir"/0001-systemd-order-remote-mounts-from-mountinfo-before-re.patch
  patch -Np1 <"$srcdir"/0001-acpi-fpdt-break-on-zero-or-negative-length-read.patch
  patch -Np1 <"$srcdir"/0001-cryptsetup-generator-auto-add-deps-for-device-as-pas.patch
  patch -Np1 <"$srcdir"/0001-do-not-accept-garbage-from-acpi-firmware-performance.patch
  patch -Np1 <"$srcdir"/0001-tmpfiles.d-include-setgid-perms-for-run-log-journal.patch

  autoreconf
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --enable-gtk-doc \
      --disable-audit \
      --disable-ima \
      --with-sysvinit-path= \
      --with-sysvrcnd-path= \
      --with-firmware-path="/usr/lib/firmware/updates:/usr/lib/firmware"

  make
}

#check() {
#  make -C "$pkgbase-$pkgver" check
#}

package() {
  pkgdesc="system and service manager"
  depends=('acl' 'bash' 'dbus-core' 'glib2' 'kbd' 'kmod' 'hwids' 'libcap' 'libgcrypt'
           'pam' 'util-linux' 'xz')
  provides=("systemd=$pkgver" "libsystemd=$pkgver" "systemd-tools=$pkgver"
            "udev=$pkgver" 'nss-myhostname'
            'libgudev-1.0.so' 'libsystemd-daemon.so' 'libsystemd-id128.so'
            'libsystemd-journal.so' 'libsystemd-login.so' 'libudev.so')
  replaces=('systemd' 'libsystemd' 'systemd-tools' 'udev' 'nss-myhostname')
  conflicts=('systemd' 'libsystemd' 'systemd-tools' 'udev' 'nss-myhostname')
  optdepends=('cryptsetup: required for encrypted block devices'
              'quota-tools: kernel-level quota management'
              'python: systemd library bindings'
              'systemd-sysvcompat: symlink package to provide sysvinit binaries')
  backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
          etc/dbus-1/system.d/org.freedesktop.hostname1.conf
          etc/dbus-1/system.d/org.freedesktop.login1.conf
          etc/dbus-1/system.d/org.freedesktop.locale1.conf
          etc/dbus-1/system.d/org.freedesktop.machine1.conf
          etc/dbus-1/system.d/org.freedesktop.timedate1.conf
          etc/pam.d/systemd-user
          etc/systemd/bootchart.conf
          etc/systemd/journald.conf
          etc/systemd/logind.conf
          etc/systemd/system.conf
          etc/systemd/user.conf
          etc/udev/udev.conf)
  install="systemd.install"

  make -C "$pkgbase-$pkgver" DESTDIR="$pkgdir" install

  printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # don't write units to /etc by default -- we'll enable this on post_install
  # as a sane default
  rm "$pkgdir/etc/systemd/system/getty.target.wants/getty@tty1.service"
  rmdir "$pkgdir/etc/systemd/system/getty.target.wants"

  # fix .so links in manpage stubs
  find "$pkgdir/usr/share/man" -type f -name '*.[[:digit:]]' \
      -exec sed -ri '1s|^\.so (.*)\.([0-9]+)|.so man\2/\1.\2|' {} +

  # get rid of RPM macros
  rm -r "$pkgdir/usr/lib/rpm"

  # add back tmpfiles.d/legacy.conf
  install -m644 "systemd-$pkgver/tmpfiles.d/legacy.conf" "$pkgdir/usr/lib/tmpfiles.d"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules

  # add mkinitcpio hooks
  install -Dm644 "$srcdir/initcpio-install-systemd" "$pkgdir/usr/lib/initcpio/install/systemd"
  install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
  install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"

  # ensure proper permissions for /var/log/journal
  chown root:systemd-journal "$pkgdir/var/log/journal"
  chmod 2755 "$pkgdir/var/log/journal"

  # fix pam file
  sed 's|system-auth|system-login|g' -i "$pkgdir/etc/pam.d/systemd-user"

  ### split out manpages for sysvcompat
  rm -rf "$srcdir/_sysvcompat"
  install -dm755 "$srcdir"/_sysvcompat/usr/share/man/man8/
  mv "$pkgdir"/usr/share/man/man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8 \
     "$srcdir"/_sysvcompat/usr/share/man/man8

  # create /var/log/journal. users can control the actual log destination via journald.conf
  install -dm755 "$pkgdir/var/log/journal"
}

# vim: ft=sh syn=sh et
