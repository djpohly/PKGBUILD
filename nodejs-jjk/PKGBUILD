# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

_pkgname=nodejs
pkgname=$_pkgname-jjk
pkgver=6.2.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl' 'zlib') # 'libuv' 'v8' 'c-ares')
makedepends=('python2' 'procps-ng')
optdepends=('npm: nodejs package manager')
conflicts=($_pkgname)
provides=($_pkgname)
source=("http://nodejs.org/dist/v$pkgver/node-v$pkgver.tar.xz"
        "node-v$pkgver.tar.xz.sha256.signed-msg::http://nodejs.org/dist/v$pkgver/SHASUMS256.txt.asc")
sha256sums=('SIGNED-MSG' 'SKIP')
validpgpkeys=('DD8F2338BAE7501E3DD5AC78C273792F7D83545D'
              '71DCFD284A79C3B38668286BC97EC7A07EDE3FC1'
              'FD3A5288F042B6850C66B31F09FE44734EB7990E'
              '0034A06D9D9B0064CE8ADF6BF1747F4AD2306D93'
              '94AE36675C464D64BAFA68DD7434390BDBE9B9C5'
              '9554F04D7259F04124DE6B476D5A82AC7E37093B'
              'C4F0DFFF4E8C1A8236409D08E73BC641CC11F4C8'
              'B9AE9905FFD7803F25714661B63B535A4C206CA9'
              )

prepare() {
  cd node-v$pkgver

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
    -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-v$pkgver

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --without-npm \
    --shared-openssl \
    --shared-zlib
    # --shared-libuv
    # --shared-v8
    # --shared-cares

  make
}

#check() {
#  cd node-v$pkgver
#  make test || warning "Tests failed"
#}

package() {
  cd node-v$pkgver

  make DESTDIR="$pkgdir" install

  # install docs as per user request
  install -d "$pkgdir"/usr/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/usr/share/doc/nodejs

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
