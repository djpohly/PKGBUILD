# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgname=xf86-video-ati
pkgname=$_pkgname-jjk
pkgver=18.0.1
pkgrel=1
pkgdesc="X.org ati video driver"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('mesa')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23')
conflicts=('xorg-server<1.19.0' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24'
           $_pkgname)
provides=($_pkgname)
source=($url/releases/individual/driver/$_pkgname-$pkgver.tar.bz2{,.sig}
        failure-to-set-drm-master-is-not-fatal.patch)
sha256sums=('72ea3b8127d4550b64f797457f5a7851a541fa4ee2cc3f345b6c1886b81714a0'
            'SKIP'
            '730f259c35bbc60a313991c8e35a3a166a61ab86b87fcfd604e865d251ae9ce9')
validpgpkeys=('B09FAF35BE914521980951145A81AF8E6ADBB200') # Michel Daenzer <michel@daenzer.net>

prepare() {
    cd $_pkgname-$pkgver
    patch -p0 -i ../failure-to-set-drm-master-is-not-fatal.patch
}

build() {
  cd $_pkgname-$pkgver

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  ./configure --prefix=/usr
  make
}

package() {
  cd $_pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$_pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/"
}
