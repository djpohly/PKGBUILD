# $Id: PKGBUILD 180193 2013-03-18 16:09:02Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgname=xf86-video-ati
pkgname=$_pkgname-jjk
pkgver=7.4.0
pkgrel=1
pkgdesc="X.org ati video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm>=2.4.41' 'systemd' 'ati-dri')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=15' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16'
           $_pkgname)
provides=($_pkgname)
groups=('xorg-drivers' 'xorg')
options=('!libtool')
source=(${url}/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2)
sha256sums=('96230ca9859a2a3e347b7f37597b7b344e80b83f378d3a8463ab1ceb69df76d9')

build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-glamor
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
