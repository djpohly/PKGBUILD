# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.15.0
pkgrel=8
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-Allow-external-use-of-strings-about-Extended-Search.patch
        0005-Add-vfolder-plugin.patch
        0006-quicksearch-expand-extended-symbols-on-Edit.patch
        0007-Add-a-signature.asc-name-for-PGP-MIME-signatures.patch
        0008-Speed-up-searches-in-References-via-x.patch
        0009-prefs-add-option-Ignore-sort-direction-Make-sort-ord.patch
        0010-Add-Delivery-Status-Notification-DSN-support.patch
        0011-Fix-Date-null-in-return-receipt-since-989b3a3f.patch)
sha256sums=('4e4d2d0d43f8ae3d4623408612f5979e9a697ccbc12038b80dd27802e868dc2e'
            'SKIP'
            '051656075f888c83053c4424c16974bb531f82bbe148790f6d39443bfaaddc1e'
            '470fea22cf815cd94d78a9b05ae5ceab2f540e85c1c4cf8982cac4e0eb8dded9'
            'b65fabc3bb87ee7ac7a3c0bc3a6f3a5dfc3e251aef2db5da12050a8dbee4629f'
            'd80fa11fab35133f6ca14d8dbc2e5ecccca0ca544ddf68a6193a21cfa4913ea8'
            '498e9a3651a343b8a35399c0a07a4d74fce211ccaa0b5323d367d51446e83d3c'
            'aff93518c1cce373928acaba53fe86be4fbfddcfa1a6738567472d18c054cba5'
            'c48c01cf903c12edb154c19c7d7a455d29adcbf08e6c48f5030f486d05ffcf26'
            'f49501b94de47ffc286e8042431ec17ae13181b6d115b66bb757aface77aea7e'
            '74150256543c65261b5ec48d8f004a5f33b557a799a0868bf8dc357f6e8eee2e'
            '757a5344fca283b0a892d923b511fe118ef25cbbc9819de62f89d9fdaa9548bc'
            'd8111e0deec8ccbbd1addd0359b20d2fba366ddb44d79a4bd351e8953d489f60')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  for f in "${source[@]}"; do
      [[ "$f" = *".patch" ]] && patch -p1 -i ../"$f"
  done
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pdf_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 "${i}" \
        "${pkgdir}/usr/lib/claws-mail/tools/${i}"
  done
}
