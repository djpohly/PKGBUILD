# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.14.1
pkgrel=4
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        claws.git-afe0203a640d39817f068e0fb9b5f2cb631cf677.patch
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-summaryview-Minor-fix.patch
        0005-prefs_filtering-Add-missing-shadow-on-the-list.patch
        0006-plugins-notification-Update-more-tray-s-items-sensit.patch
        0007-Add-vfolder-plugin.patch
        0008-Fix-memory-leak.patch)
sha256sums=('b79a1dc04f0b3d1f693e84d7fa7282f140fb5280179281445a8b954ce423c335'
            'SKIP'
            '49ecbb30849a519c27f12d1c8bc2d290d8c2d857438639b1f6f071fb5e788291'
            '29c6eef066aa46b6f62b06b776e41d66e4b423db47f5c2b79505ba8629878821'
            'ee516b4483009ce7e8fa91a8f950fc62c5299bbdce58701e10cd3ce03f018092'
            'a133e951c29005b555f5b672c5edee5015d3c72a253c36467bfd74c2b9dadd04'
            '7d526cb072eb9bd52bd50809f738450de82e42e1170177aecaf93ec5f90bfbf0'
            'b7bad39eebf7245981aa45a53ad8f40a91e7fddbe44ee18d0648b3720b68ee60'
            'a7732321ece46382e9fde24afd6bb6746f7149287e90d55e93251c9d8cef9ca4'
            '6a935cc6c3a2e882613f7d211bb088f2089102b224818b2fbc00bdb4d81ca7d2'
            '8ea355695f62dc4ed6c4825fcd56ce223522721a1f52a10a2d2f803fe156c606')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i ../claws.git-afe0203a640d39817f068e0fb9b5f2cb631cf677.patch
  patch -p1 -i ../0001-Fix-diff-format-detection.patch
  patch -p1 -i ../0002-Add-coloring-of-diff-patches-inside-emails.patch
  patch -p1 -i ../0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
  patch -p1 -i ../0004-summaryview-Minor-fix.patch
  patch -p1 -i ../0005-prefs_filtering-Add-missing-shadow-on-the-list.patch
  patch -p1 -i ../0006-plugins-notification-Update-more-tray-s-items-sensit.patch
  patch -p1 -i ../0007-Add-vfolder-plugin.patch
  patch -p1 -i ../0008-Fix-memory-leak.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pfd_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 "${i}" \
        "${pkgdir}/usr/lib/claws-mail/tools/${i}"
  done
}
