# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.15.0
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-Add-vfolder-plugin.patch)
sha256sums=('4e4d2d0d43f8ae3d4623408612f5979e9a697ccbc12038b80dd27802e868dc2e'
            'SKIP'
            'd6a2a329be22a30052df4451b4ce76d275902409522c1a98c6c88eac4c9bae80'
            'b67e481d4c7665ef2cde176c6fdd5f4c7e0822b860bd3ba12c4918f549a6818c'
            '65dd62f1c2947236084e0e528c805b17011edc5d1e99eead3cebec0d269b5429'
            '4dc1b46127f0a863de21a81304ae6e7d59275aa90492dbb253f97d6c18579471')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i ../0001-Fix-diff-format-detection.patch
  patch -p1 -i ../0002-Add-coloring-of-diff-patches-inside-emails.patch
  patch -p1 -i ../0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
  patch -p1 -i ../0004-Add-vfolder-plugin.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pfd_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 "${i}" \
        "${pkgdir}/usr/lib/claws-mail/tools/${i}"
  done
}
