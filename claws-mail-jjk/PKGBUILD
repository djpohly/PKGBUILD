# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.15.0
pkgrel=6
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-Allow-external-use-of-strings-about-Extended-Search.patch
        0005-Add-vfolder-plugin.patch
        0006-quicksearch-expand-extended-symbols-on-Edit.patch
        0007-prefs-add-option-Ignore-sort-direction-Make-sort-ord.patch
        0008-Add-Delivery-Status-Notification-DSN-support.patch
        0009-Fix-Date-null-in-return-receipt-since-989b3a3f.patch)
sha256sums=('4e4d2d0d43f8ae3d4623408612f5979e9a697ccbc12038b80dd27802e868dc2e'
            'SKIP'
            '9934dd6e323836fa8381312d9f9784c6d047fa78de39b8ad2ea130e3aa415d0e'
            '47a38b38fd5b79c0473ad942faa143c0cf6d5dd22e084e262510456ea6eb4603'
            'b755811c12a96bcb6c5709f90e8dfa09d4719184fc9c68f0cc51970d8dd3a140'
            'af10f4b7e952c118eaa71fc8a22161e8f6aef144d981770b78f603f55a01439c'
            '6a49bb6d1ad988c7c83969115a05d4c48058f3c6acd993449334ad43e7b01b1f'
            '5a6ec17e4c37fafebfa77453c9bf2eb476941453a22a8b899c87de5acd505f7b'
            '9dfd84369736194f77976b915a84e9ae1317d628796065b9fe853859ffa32412'
            '61895d56345fa89c2266e4c9316bbbd5d2e9c76353c74efeda0ec7bf74377b21'
            '005372582c0e6982466f93773ac1f27b8dbd22dee464c302fcb374dc7de04561')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i ../0001-Fix-diff-format-detection.patch
  patch -p1 -i ../0002-Add-coloring-of-diff-patches-inside-emails.patch
  patch -p1 -i ../0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
  patch -p1 -i ../0004-Allow-external-use-of-strings-about-Extended-Search.patch
  patch -p1 -i ../0005-Add-vfolder-plugin.patch
  patch -p1 -i ../0006-quicksearch-expand-extended-symbols-on-Edit.patch
  patch -p1 -i ../0007-prefs-add-option-Ignore-sort-direction-Make-sort-ord.patch
  patch -p1 -i ../0008-Add-Delivery-Status-Notification-DSN-support.patch
  patch -p1 -i ../0009-Fix-Date-null-in-return-receipt-since-989b3a3f.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pfd_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 "${i}" \
        "${pkgdir}/usr/lib/claws-mail/tools/${i}"
  done
}
