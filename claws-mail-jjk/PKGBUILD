# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.16.0
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-Allow-external-use-of-strings-about-Extended-Search.patch
        0005-Add-vfolder-plugin.patch
        0006-quicksearch-expand-extended-symbols-on-Edit.patch
        0007-Add-a-signature.asc-name-for-PGP-MIME-signatures.patch
        0008-Speed-up-searches-in-References-via-x.patch
        0009-prefs-add-option-Ignore-sort-direction-Make-sort-ord.patch
        0010-Add-Delivery-Status-Notification-DSN-support.patch)
sha256sums=('c212133d3d962129d8c7995c1c7d973faef2a8914481c0c74578217bf4c897ab'
            'SKIP'
            'dfe835c372ae08808374e2f468d4c2b43eeab3d52894ce873812cd8649327abf'
            '2572b48196ac633a08a809353b483b4381430467faca85f959b16e0dfe7d60ce'
            'c0cb84ee50202eb9cea25585e396123027e26bd6d7364a76c10dbfd309496073'
            '0f6cc31867fd5f1e693a2a445fc10bd8978920f2733de09a1182a547d64e2bcb'
            '997f42afa4af1f1b6d26142eaa5c939f247cf1849e4f27a04d457ead77f7e57f'
            '23db8ab10908306d010e71d480ed624d6c2d592fa0b8ff0d115d1810c1b45e21'
            '452564426bdb1dbe0f40ee5eb2018260a67f37a392ea55d921a44563efe1d9d9'
            '6e69e4ab28ccffb42ccd3356c95ce2f91ccd603bfbca8bd1caaca2c06899c84e'
            'cec2888c7f167e29a20c93ad802ad38a694ec207ad373653f24993d24443acc8'
            'ebeb325037cbfd7ae6d4500d5ee3e2040510c75305edc53228947f018db4e4c0')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  for f in "${source[@]}"; do
      [[ "$f" = *".patch" ]] && patch -p1 -i ../"$f"
  done
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pdf_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install extra tools
  cd tools
  install -m755 -d "$pkgdir"/usr/lib/claws-mail/tools
  for file in *.pl *.py *.sh multiwebsearch.conf tb2claws-mail update-po \
      uudec uuooffice README; do
      cp -av "$file" "$pkgdir"/usr/lib/claws-mail/tools
  done
}
