# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=claws-mail
pkgname=$_pkgname-jjk
pkgver=3.15.1
pkgrel=4
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
options=('debug' '!strip')
depends=('gtk2' 'gnutls' 'startup-notification' 'gpgme' 'libetpan'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('valgrind'
             # dependencies for plugins
             'libnotify' 'pygtk') 
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'dbus:              for notification plugin')
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=("$_pkgname" 'claws-mail-extra-plugins')
provides=("$_pkgname" 'claws')
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz{,.asc}
        0001-Fix-diff-format-detection.patch
        0002-Add-coloring-of-diff-patches-inside-emails.patch
        0003-Always-sort-by-date-ASC-within-a-thread-when-by-thre.patch
        0004-Allow-external-use-of-strings-about-Extended-Search.patch
        0005-Add-vfolder-plugin.patch
        0006-quicksearch-expand-extended-symbols-on-Edit.patch
        0007-Add-a-signature.asc-name-for-PGP-MIME-signatures.patch
        0008-Speed-up-searches-in-References-via-x.patch
        0009-prefs-add-option-Ignore-sort-direction-Make-sort-ord.patch
        0010-Add-Delivery-Status-Notification-DSN-support.patch
        0011-Fix-Date-null-in-return-receipt-since-989b3a3f.patch)
sha256sums=('8d093c2f32db863c1141d56e35424c04ee48fe5d6adf4c7f349f647fa3149542'
            'SKIP'
            '1c1e0e664c781866822028ad5f3bf30458161621744caf07a9b185725f67a38d'
            'a07a86d707a3449612c633f194d1e83a466b6febaab530dbe93533c2ca430c2e'
            '1a3817f68d39652ce0eb42258c340d247e58905c70986bee13c3009a346a62fd'
            'e4a99fc05b29f226e5f042b01fbe6f1162a9ee0c8a63440ab65e8d45b16ed809'
            'f91b557e75a4f640e4d329af257a67348f5f855c3b8dd9ac604320fab3acce67'
            'ab5d8b6b49e82d6bc6f78e4a1c02807772f096603ac4654adcbb797ba8c00e8f'
            '48d8d24fc5f6d75da55a05a2001ecade0e032bb36174e4a8bb123eed58d2a884'
            '58e635386c84f0c83738287eab57732d64fee102d4ba51facdd99daa32ba156a'
            '9cd9ddaf4f6e68bbaf7fdeb05d4831ae733e20bb1b2ce70cbe49bc7bbffc5479'
            'a75e1606d131f2e25218cc9e8f2b90481386698e72264b440eaa210e38f9c879'
            'f76d69573ab4f57a76cb536567cf852ec652bf382e2583d7934495dedd6a25f1')
validpgpkeys=('8B3B297A03468356692F8D592CD716D654D6BBD4') # Paul <paul@claws-mail.org>

prepare() {
  cd "${_pkgname}-${pkgver}"
  for f in "${source[@]}"; do
      [[ "$f" = *".patch" ]] && patch -p1 -i ../"$f"
  done
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${_pkgname}-${pkgver}"

  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure

  ./configure --prefix=/usr --disable-static \
    --disable-enchant \
    --disable-compface \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-att_remover-plugin \
    --enable-attachwarner-plugin \
    --enable-bogofilter-plugin \
    --enable-notification-plugin \
    --enable-pgpcore-plugin \
    --enable-pgpmime-plugin \
    --enable-python-plugin \
    --enable-vfolder-plugin \
    --disable-acpi_notifier-plugin \
    --disable-address_keeper-plugin \
    --disable-archive-plugin \
    --disable-bsfilter-plugin \
    --disable-clamd-plugin \
    --disable-fancy-plugin \
    --disable-fetchinfo-plugin \
    --disable-gdata-plugin \
    --disable-libravatar-plugin \
    --disable-mailmbox-plugin \
    --disable-managesieve-plugin \
    --disable-pdf_viewer-plugin \
    --disable-rssyl-plugin \
    --disable-spamassassin-plugin \
    --disable-spam_report-plugin \
    --disable-tnef_parse-plugin \
    --disable-vcalendar-plugin
  make
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install extra tools
  cd tools
  install -m755 -d "$pkgdir"/usr/lib/claws-mail/tools
  for file in *.pl *.py *.sh multiwebsearch.conf tb2claws-mail update-po \
      uudec uuooffice README; do
      cp -av "$file" "$pkgdir"/usr/lib/claws-mail/tools
  done
}
