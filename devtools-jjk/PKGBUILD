# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

_pkgname=devtools
pkgname=$_pkgname-jjk
pkgver=20141224
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        0001-makechrootpkg-Add-P-for-private-network.patch
        0001-makechrootpkg-Copy-back-the-PKGBUILD-in-case-pkgver-.patch)
md5sums=('9b6e6ce9da5b8e861c77f9badc652553'
         '4ffdaee2bd45379e2e09113f02c5ea3b'
         '8923755d8d5ca7c48ee04666b252eb07')

prepare() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -p1 -i ../0001-makechrootpkg-Add-P-for-private-network.patch
    patch -p1 -i ../0001-makechrootpkg-Copy-back-the-PKGBUILD-in-case-pkgver-.patch
}

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make PREFIX=/usr
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install
}
