# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

_pkgname=devtools
pkgname=$_pkgname-jjk
pkgver=20141024
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        0001-makechrootpkg-Add-P-for-private-network.patch
        0001-makechrootpkg-Copy-back-the-PKGBUILD-in-case-pkgver-.patch)
md5sums=('16366a5a015315392b90e31ea96825cc'
         '91387cd753b15febf213df5cfba92bca'
         '8d20961cdebcfbcaea88e28108cbbfbb')

prepare() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -p1 -i ../0001-makechrootpkg-Add-P-for-private-network.patch
    patch -p1 -i ../0001-makechrootpkg-Copy-back-the-PKGBUILD-in-case-pkgver-.patch
}

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make PREFIX=/usr
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install
}
