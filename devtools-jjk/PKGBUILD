# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

_pkgname=devtools
pkgname=$_pkgname-jjk
pkgver=20150228
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("https://sources.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.sig}
        0001-makechrootpkg-Add-P-for-private-network.patch
        0001-makechrootpkg-Copy-back-the-PKGBUILD-in-case-pkgver-.patch)
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53')
md5sums=('4b17a83dac3f8c93de85acebd0a90a16'
         'SKIP'
         '3ef3b5a2e307d85ee3271a86234db3d8'
         '36c1589b1678189065cd6fe5a1947cff')

prepare() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -p1 -i ../0001-makechrootpkg-Add-P-for-private-network.patch
    patch -p1 -i ../0001-makechrootpkg-Copy-back-the-PKGBUILD-in-case-pkgver-.patch
}

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make PREFIX=/usr
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install
}
