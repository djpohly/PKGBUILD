# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

_pkgname=devtools
pkgname=$_pkgname-jjk
pkgver=20140510
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        0001-makechrootpkg-Add-P-for-private-network.patch)
md5sums=('702376263627e594b5a7ff1970353de4'
         '3bc6faf64087d8d479098bdc8af7e687')

prepare() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	patch -p1 -i ../0001-makechrootpkg-Add-P-for-private-network.patch
}

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make PREFIX=/usr
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
