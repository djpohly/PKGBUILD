# Maintainer: Ced <cedl38@gmail.com>

_pkgname=zen-gtk-themes
pkgname=zen-blua-jjk
pkgver=0.12.0
pkgrel=1
pkgdesc="A GTK2/GTK3 theme that attempt to keep it simple, consistent and being visually appealing."
arch=('any')
url="http://opendesktop.org/content/show.php/Zen+suite?content=149883"
license=('GPL3')
depends=('gtk-engines' 'gtk-engine-murrine')

source=("$_pkgname-$pkgver.tar.gz::https://github.com/cedl38/$_pkgname/archive/v$pkgver.tar.gz"
        jjk-tweak-titlebar-colors.patch)
sha256sums=('868266dca1f347e48ee19abbe3e07c8250777cb988120cfd8dbd3ae2e3925ec2'
            '99eb5b2fb2a1475090037ca131f144105e15031b37220f344b2dd5facf8a054d')

build() {
    cd "$srcdir/$_pkgname-$pkgver"
    ./configure --prefix=/usr --disable-debug
    make
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install

    # not sure how this is supposed to work, but apparently make removes files
    # from Zen-blua and copies over files from Zen; so we can't apply our patch
    # in prepare()
    # But, `make install` actually does the same as make before doing the
    # install part, so really the only place we can patch is here:
    cd "$pkgdir/usr/share/themes"
    patch -Np0 -i "$srcdir/jjk-tweak-titlebar-colors.patch"
    cd -

    rm -rf "$pkgdir"/usr/share/themes/Zen{,-{bicolor,griza,nigra,nokto,papero,verda}}
    rm -rf "$pkgdir"/usr/share/themes/Zen-blua/xfce-notify-4.0
    mv "$pkgdir"/usr/share/themes/{Zen-blua,$pkgname}
}
