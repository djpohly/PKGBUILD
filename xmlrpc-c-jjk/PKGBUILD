# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

_pkgname=xmlrpc-c
pkgname=$_pkgname-jjk
pkgver=1.32.2
pkgrel=1
pkgdesc='XML-RPC for C and C++'
arch=('x86_64' 'i686')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
provides=($_pkgname)
conflicts=($_pkgname)
depends=('curl' 'libxml2')
makedepends=('libtool' 'setconf')
options=('!makeflags' '!libtool' '!emptydirs')
source=("$_pkgname-advanced-$pkgver.tar.gz::http://xmlrpc-c.svn.sourceforge.net/viewvc/xmlrpc-c/advanced.tar.gz")
sha256sums=('52e11b27e5c273d2d06eb4dd4e40019756abb51ab099aa3b35a2094944488df3')

build() {
  cd "$srcdir/advanced"

  [ "$CARCH" != "i686" ] && export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-libxml2-backend \
    --disable-cgi-server \
    --disable-abyss-server \
    --disable-libwww-client \
    --disable-wininet-client
  mkdir include/curl
  touch include/curl/types.h
  make CFLAGS_PERSONAL="$CFLAGS"
  # build the xmlrpc binary as well
  make CFLAGS_PERSONAL="$CFLAGS" -C tools/xmlrpc
}

package() {
  cd "$srcdir/advanced"

  make DESTDIR="$pkgdir" install
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  # Remove references to build directory
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" BLDDIR ""
  setconf "$pkgdir/usr/bin/xmlrpc-c-config" ABS_SRCDIR ""
  # install the xmlrpc binary
  install -Dm755 tools/xmlrpc/xmlrpc "$pkgdir/usr/bin/xmlrpc"
}

# vim:set ts=2 sw=2 et:
