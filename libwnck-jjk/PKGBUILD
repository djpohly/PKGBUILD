# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>
_pkgname=libwnck
pkgname=$_pkgname-jjk
pkgver=2.31.0
pkgrel=1
pkgdesc="Window Navigator Construction Kit"
arch=('x86_64')
license=('LGPL')
depends=('gtk2' 'startup-notification' 'libxres')
makedepends=('libxt' 'intltool' 'gobject-introspection' 'python2')
provides=($_pkgname)
conflicts=($_pkgname)
url="http://www.gnome.org/"
source=(https://download.gnome.org/sources/libwnck/${pkgver:0:4}/libwnck-$pkgver.tar.xz
        libwnck-$pkgver.tar.xz.sha256::https://download.gnome.org/sources/libwnck/${pkgver:0:4}/libwnck-$pkgver.sha256sum
        WindowActionMenu-unset-window-and-stop-async-events-on-dispose.patch
        0001-window-action-menu-Add-item-to-send-SIGSTOP-SIGCONT.patch)
sha256sums=('FILE'
            '723397314d68ba371c777927fafc91934a440c0f739cf3bd8180e666e4bbbcb3'
            'a4a359027f3297847e7d3c9d8cefc6404a927ad6fb5aed864f58524834c60980'
            'b2d2e3abb84b022620e8c834b321ec9ef7aa5b434216bdad2e8215b6d5baf066')

prepare() {
    cd libwnck-$pkgver
    patch -Np1 -i ../WindowActionMenu-unset-window-and-stop-async-events-on-dispose.patch
    patch -Np1 -i ../0001-window-action-menu-Add-item-to-send-SIGSTOP-SIGCONT.patch
}

build() {
    cd libwnck-$pkgver
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd libwnck-${pkgver}
    make DESTDIR="$pkgdir" install

    #The binaries are shipped in libwnck3
    rm -f "$pkgdir/usr/bin/wnckprop"
    rm -f "$pkgdir/usr/bin/wnck-urgency-monitor"
    rmdir "$pkgdir/usr/bin"
}
