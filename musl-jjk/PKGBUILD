# $Id$
# Maintainer:  Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: TJ Vanderpoel <tj@rubyists>

_pkgname=musl
pkgname=$_pkgname-jjk
pkgver=1.1.14
pkgrel=1
pkgdesc='Lightweight implementation of C standard library'
arch=('i686' 'x86_64')
url='http://www.musl-libc.org/'
license=('MIT')
options=('staticlibs' '!buildflags')
provides=($_pkgname)
conflicts=($_pkgname)
source=(http://www.musl-libc.org/releases/musl-$pkgver.tar.gz{,.asc})
validpgpkeys=('836489290BB6B70F99FFDA0556BCDB593020450F')
sha1sums=('b71208e87e66ac959d0e413dd444279d28a7bff1'
          'SKIP')

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr/lib/musl \
    --exec-prefix=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/lib/
  mv "$pkgdir"/lib/ld-musl*.so* "$pkgdir"/usr/lib/
  rmdir "$pkgdir"/lib

  install -Dm0644 README "$pkgdir"/usr/share/doc/musl/README
  install -Dm0644 COPYRIGHT "$pkgdir"/usr/share/licenses/musl/COPYRIGHT
}
