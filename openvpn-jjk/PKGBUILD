# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>

_pkgname=openvpn
pkgname=$_pkgname-jjk
pkgver=2.4.4
pkgrel=1
pkgdesc='An easy-to-use, robust and highly configurable VPN (Virtual Private Network)'
arch=('x86_64')
url='https://openvpn.net/index.php/open-source.html'
depends=('openssl' 'lzo' 'iproute2' 'pkcs11-helper')
optdepends=('easy-rsa: easy CA and certificate handling'
            'pam: authenticate via PAM')
provides=($_pkgname)
conflicts=($_pkgname)
license=('custom')
validpgpkeys=('F554A3687412CFFEBDEFE0A312F5F7B42F2B01E7') # OpenVPN - Security Mailing List <security@openvpn.net>
source=("https://swupdate.openvpn.net/community/releases/openvpn-$pkgver.tar.xz"{,.asc}
        0001-Fix-route_vpn_gateway-not-always-set.patch)
sha256sums=('96cd1b8fe1e8cb2920f07c3fd3985faea756e16fdeebd11d3e146d5bd2b04a80'
            'SKIP'
            'd61b1fd3c316319994e2a48f3c9657513afd00d679ffd60feb0f03bffb5b9cf7')

prepare() {
  cd openvpn-$pkgver
  patch -p1 -i ../0001-Fix-route_vpn_gateway-not-always-set.patch
}

build() {
  cd openvpn-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-iproute2 \
    --enable-pkcs11 \
    --enable-plugins \
    --enable-x509-alt-username
  make
}

check() {
  cd openvpn-$pkgver
  make check
}

package() {
  cd openvpn-$pkgver

  # Install openvpn
  make DESTDIR="$pkgdir" install

  # Install examples
  install -d -m0755 "$pkgdir"/usr/share/openvpn
  cp -r sample/sample-config-files "$pkgdir"/usr/share/openvpn/examples

  # Install license
  install -d -m0755 "$pkgdir"/usr/share/licenses/openvpn/
  ln -sf /usr/share/doc/openvpn/{COPYING,COPYRIGHT.GPL} "$pkgdir"/usr/share/licenses/openvpn/

  # Install contrib
  for FILE in $(find contrib -type f); do
    case "$(file --brief --mime-type "$FILE")" in
      "text/x-shellscript") install -D -m0755 "$FILE" "$pkgdir/usr/share/openvpn/$FILE" ;;
      *) install -D -m0644 "$FILE" "$pkgdir/usr/share/openvpn/$FILE" ;;
    esac
  done
}
