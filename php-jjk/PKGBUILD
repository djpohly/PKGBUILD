# $Id: PKGBUILD 158788 2012-05-09 18:48:29Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=php
pkgname=('php-jjk'
         'php-fpm-jjk')
pkgver=7.0.2
pkgrel=2
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('db' 'gmp'
             'libxml2' 'bzip2' 'curl')
source=("http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.xz"{,.asc}
        'php.ini.patch' 'php-fpm.patch' 'bug-71269.patch')
validpgpkeys=('1A4E8B7277C42E53DBA9C7B9BCAA30EA9C0D5763'
              '6E4F6AB321FDC07F2C332E3AC2BF0BC433CFC8B3')
sha256sums=('556121271a34c442b48e3d7fa3d3bbb4413d91897abbb92aaeced4a7df5f2ab2'
            'SKIP'
            '2586f0e1406849a526453b0ea505f57b2b5ced33b1cbd781f3533ef8f75e4b82'
            '896010fede8cfd0fc7ca04b42aea39597b34054b699507a3ebc7f303f7b8c8b3'
            '4fc93498a9493b5b2b33675213f4f4323c83f0dd5887fbd5af38d4762d8d2ff8')

prepare() {
    cd "${srcdir}/${pkgbase}-${pkgver}"

    patch -p0 -i ../php.ini.patch
    patch -p0 -i ../php-fpm.patch
    patch -p1 -i ../bug-71269.patch
}

build() {
    phpconfig="--srcdir=../${pkgbase}-${pkgver} \
        --config-cache \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc/php \
        --localstatedir=/var \
        --with-layout=GNU \
        --with-config-file-path=/etc/php \
        --with-config-file-scan-dir=/etc/php/conf.d \
        --disable-rpath \
        --mandir=/usr/share/man \
        --without-pear \
        --enable-zend-signals \
        "

    phpextensions="--enable-bcmath=shared \
        --enable-calendar=shared \
        --enable-dba=shared \
        --enable-exif=shared \
        --enable-ftp=shared \
        --enable-mbstring \
        --enable-shmop=shared \
        --enable-soap=shared \
        --enable-sockets=shared \
        --enable-sysvmsg=shared \
        --enable-sysvsem=shared \
        --enable-sysvshm=shared \
        --enable-zip=shared \
        --with-bz2=shared \
        --with-curl=shared \
        --with-db4=/usr \
        --with-freetype-dir=/usr \
        --with-gd=shared \
        --with-gdbm \
        --with-gettext=shared \
        --with-gmp=shared \
        --with-iconv=shared \
        --with-mhash \
        --with-openssl \
        --with-pcre-regex=/usr \
        --with-xmlrpc=shared \
        --with-zlib \
        --enable-pcntl \
        "

    EXTENSION_DIR=/usr/lib/php/modules
    export EXTENSION_DIR

    mkdir "${srcdir}/build"
    cd "${srcdir}/build"
    ln -s ../${pkgbase}-${pkgver}/configure
    ./configure ${phpconfig} \
        --enable-cgi \
        --enable-fpm \
        --with-fpm-user=http \
        --with-fpm-group=http \
        ${phpextensions}
    make
}

package_php-jjk() {
	pkgdesc='An HTML-embedded scripting language'
	depends=('libxml2' 'bzip2' 'curl')
	provides=('php')
	conflicts=('php')
	backup=('etc/php/php.ini')

	cd "${srcdir}/build"
	make -j1 INSTALL_ROOT="${pkgdir}" install-{modules,cli,build,headers,programs,pharcmd}
	# install php.ini
	install -D -m644 "${srcdir}"/${pkgbase}-${pkgver}/php.ini-production "${pkgdir}"/etc/php/php.ini
	install -d -m755 "${pkgdir}"/etc/php/conf.d/

	# remove static modules
	rm -f "${pkgdir}"/usr/lib/php/modules/*.a
	# remove modules provided by sub packages
	rm -f "${pkgdir}"/usr/lib/php/modules/{enchant,gd,intl,ldap,mcrypt,mssql,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
	# remove empty directory
	rmdir "${pkgdir}"/usr/include/php/include
}

package_php-fpm-jjk() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php')
	conflicts=('php-fpm')
	backup=('etc/php/php-fpm.conf' 'etc/php/php-fpm.d/www.conf')
    options=('!emptydirs')

    cd "${srcdir}"/build
    make -j1 INSTALL_ROOT="${pkgdir}" install-fpm
}
