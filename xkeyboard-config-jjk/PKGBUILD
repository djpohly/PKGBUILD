# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgname=xkeyboard-config
pkgname=$_pkgname-jjk
pkgver=2.11
pkgrel=1
pkgdesc="X keyboard configuration files"
arch=(any)
license=('custom')
url="http://www.freedesktop.org/wiki/Software/XKeyboardConfig"
makedepends=('intltool' 'xorg-xkbcomp')
provides=('xkbdata' $_pkgname)
replaces=('xkbdata'$_pkgname)
conflicts=('xkbdata' $_pkgname)
source=(http://xorg.freedesktop.org/archive/individual/data/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2
       cf93beb4.patch)
sha256sums=('e7125460892c2b5c3a8d843cb18c24b60c46051e925c2888a61fa672a2f76d76'
            '885c1adaf0cdb82812c1c580d659108f768d8deefe996eb8d3c538a832d4b542')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Rp1 -i ../cf93beb4.patch
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-xkb-base=/usr/share/X11/xkb \
      --with-xkb-rules-symlink=xorg \
      --enable-compat-rules=yes
  make
 }

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/X11/xkb/compiled"
  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
