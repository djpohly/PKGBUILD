# $Id: PKGBUILD 76652 2012-09-25 18:40:48Z bpiotrowski $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Bart≈Çomiej Piotrowski <barthalion@gmal.com>
# Contributor: Miroslaw Szot <mss@czlug.icis.pcz.pl>

_cfgdir=/etc/nginx
_tmpdir=/var/lib/nginx

_pkgname=nginx
pkgname=$_pkgname-jjk
pkgver=1.4.1
pkgrel=2
pkgdesc="lightweight HTTP server and IMAP/POP3 proxy server"
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl')
provides=('nginx')
conflicts=('nginx')
url="http://nginx.org"
license=('custom')
backup=(${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        etc/logrotate.d/nginx)
source=(http://nginx.org/download/nginx-$pkgver.tar.gz
        service
        logrotate)
install=install
md5sums=('fea7dfab995545ce27fe4c49dc21a972'
         'ce9a06bcaf66ec4a3c4eb59b636e0dfd'
         '7711fc234ef2f35151885c1994f8fe73')

build() {
    cd "$srcdir"/$_pkgname-$pkgver

    ./configure \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/nginx.conf \
        --sbin-path=/usr/bin/nginx \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --user=http --group=http \
        --http-log-path=/var/log/nginx/access.log \
        --error-log-path=/var/log/nginx/error.log \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-file-aio \
        --with-http_gzip_static_module
#        --with-imap --with-imap_ssl_module \
#        --with-ipv6 --with-pcre-jit \
#        --with-http_dav_module \
#        --with-http_realip_module \
#        --with-http_ssl_module \
#        --with-http_stub_status_module \
#        --add-module=/usr/lib/passenger/ext/nginx \
        #--with-http_mp4_module \
        #--with-http_realip_module \
        #--with-http_addition_module \
        #--with-http_xslt_module \
        #--with-http_image_filter_module \
        #--with-http_geoip_module \
        #--with-http_sub_module \
        #--with-http_flv_module \
        #--with-http_random_index_module \
        #--with-http_secure_link_module \
        #--with-http_degradation_module \
        #--with-http_perl_module \

    make
}

package() {
    cd "$srcdir"/$_pkgname-$pkgver
    make DESTDIR="$pkgdir" install

    sed -e 's|\<user\s\+\w\+;|user html;|g' \
        -e '44s|html|/usr/share/nginx/html|' \
        -e '54s|html|/usr/share/nginx/html|' \
        -i "$pkgdir"/etc/nginx/nginx.conf
    rm "$pkgdir"/etc/nginx/*.default

    install -d "$pkgdir"/$_tmpdir

    install -d "$pkgdir"/usr/share/nginx
    mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

    install -Dm644 "$srcdir"/logrotate "$pkgdir"/etc/logrotate.d/nginx
    install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/nginx/LICENSE
    rm -rf "$pkgdir"/var/run
    chmod 750 "$pkgdir"/var/log/nginx
    chown http:log "$pkgdir" /var/log/nginx
}
