# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-notifyd
pkgname=$_pkgname-jjk
pkgver=0.2.4
pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool')
provides=('notification-daemon' $_pkgname)
conflicts=($_pkgname)
source=(http://archive.xfce.org/src/apps/$_pkgname/0.2/$_pkgname-$pkgver.tar.bz2)
sha256sums=('8c7ed62f9496816d1391281f77d1b32216f9bf6fd22fbe4f6f3f4e07a6bbced0')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
