# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

_pkgname=wget
pkgname=$_pkgname-jjk
pkgver=1.18
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('openssl' 'libidn' 'libutil-linux')
checkdepends=('perl-http-daemon' 'perl-io-socket-ssl' 'python')
optdepends=('ca-certificates: HTTPS downloads')
provides=($_pkgname)
conflicts=($_pkgname)
backup=('etc/wgetrc')
source=(ftp://ftp.gnu.org/gnu/$_pkgname/$_pkgname-$pkgver.tar.xz{,.sig})
sha1sums=('02d451e658f600ee519c42cbf4d3bfe4e49b6c4f'
          'SKIP')
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')

prepare() {
  cd $_pkgname-$pkgver
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-nls --with-ssl=openssl
  make
}

check() {
  cd $_pkgname-$pkgver
  make check
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
