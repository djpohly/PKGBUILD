# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

_pkgname=cairo
pkgname=$_pkgname-jjk
pkgver=1.14.0
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
makedepends=('mesa' 'libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc'
             'valgrind')
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2'
         'libgl' 'sh' 'lzo')
provides=('cairo-xcb' 'cairo')
replaces=('cairo-xcb' 'cairo')
conflicts=('cairo')
options=('!libtool')
source=(http://cairographics.org/releases/$_pkgname-$pkgver.tar.xz)
sha1sums=('53cf589b983412ea7f78feee2e1ba9cea6e3ebae')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # fix compilew/ gcc 4.9 -- see FS#40313
  # upstream https://bugs.freedesktop.org/show_bug.cgi?id=77060
  CFLAGS="$CFLAGS -fno-lto"
  export CFLAGS

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --enable-tee \
      --enable-gl \
      --enable-egl \
      --enable-svg \
      --enable-ps \
      --enable-pdf \
      --enable-gobject #\
      # --enable-test-surfaces

  #--disable-xlib-xcb \
      # --enable-test-surfaces \ takes ages
  #--enable-drm # breaks build

  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
