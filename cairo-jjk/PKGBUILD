# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

_pkgname=cairo
pkgname=$_pkgname-jjk
pkgver=1.12.14
pkgrel=2
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
makedepends=('mesa>=9.1' 'libgl>=9.1' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc'
             'valgrind')
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'libgl' 'sh')
provides=('cairo-xcb' 'cairo')
replaces=('cairo-xcb' 'cairo')
conflicts=('cairo')
options=('!libtool')
source=(http://cairographics.org/releases/$_pkgname-$pkgver.tar.xz)
sha1sums=('9106ab09b2e7b9f90521b18dd4a7e9577eba6c15')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --enable-tee \
      --enable-gl \
      --enable-egl \
      --enable-svg \
      --enable-ps \
      --enable-pdf \
      --enable-gobject #\
      # --enable-test-surfaces

  #--disable-xlib-xcb \
      # --enable-test-surfaces \ takes ages
  #--enable-drm # breaks build

  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
