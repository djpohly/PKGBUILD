# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlonux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgname=libva
pkgname=$_pkgname-jjk
pkgver=2.1.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('i686' 'x86_64')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=('glibc' 'libdrm' 'libgl' 'libx11' 'libxext' 'libxfixes')
makedepends=('mesa')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
provides=($_pkgname 'libva-drm.so' 'libva-glx.so' 'libva-x11.so' 'libva.so')
conflicts=($_pkgname)
source=("https://github.com/01org/libva/releases/download/${pkgver}/libva-${pkgver}.tar.bz2")
sha256sums=('f3fa953a11d3210c3a4ee79031abdbe0863d5ce13d9b3f93f315f1eec60a4b0f')

build() {
  cd libva-${pkgver}

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd libva-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libva
}

# vim: ts=2 sw=2 et:
