# Maintainer: jjacky
_pkgname=pkgclip
pkgname=$_pkgname-git
pkgver=
pkgrel=1
pkgdesc="Cached Packages Trimmer Utility"
arch=('i686' 'x86_64')
url="http://jjacky.com/$_pkgname"
license=('GPL3+')
depends=('dbus' 'polkit' 'gtk3' 'pacman>=4.1')
makedepends=('perl' 'git' 'autoconf')
source=("git+https://github.com/jjk-jacky/${_pkgname}.git#branch=next")
md5sums=('SKIP')
provides=($_pkgname)
conflicts=($_pkgname)

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --abbrev=4 --dirty | tr - .
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh
  ./configure --prefix=/usr --enable-git-version
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
