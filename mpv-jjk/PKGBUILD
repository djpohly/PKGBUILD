# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

_pkgname=mpv
pkgname=$_pkgname-jjk
pkgver=0.27.2
pkgrel=1
_waf_version=1.9.8
pkgdesc='a free, open source, and cross-platform media player'
arch=('i686' 'x86_64')
license=('GPL')
url='https://mpv.io'
depends=(
  'ffmpeg' 'lcms2' 'libgl' 'libass' 'lua52'
  'libxss' 'libxinerama' 'libxv' 'libva' 'libdvdnav'
  'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils'
  'libxrandr'
)
makedepends=('mesa' 'python-docutils' 'ladspa')
optdepends=('youtube-dl: for video-sharing websites playback')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!emptydirs')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/mpv-player/$_pkgname/archive/v$pkgver.tar.gz"
  0002-vaapi-Use-libva2-message-callbacks.patch
  0003-demux_lavf-return-AVERROR_EOF-on-file-end.patch
  "https://waf.io/waf-$_waf_version")
sha256sums=('2ad104d83fd3b2b9457716615acad57e479fd1537b8fc5e37bfe9065359b50be'
            '3c3517f4f4c71e39e1e04ea440688fc8d7b3dc55e6bc0a9398d11a9b75bde07d'
            '5de6c616428c87cf9b39d8ba24446d65d175050c083e1054194d93cf03d5816a'
            '167dc42bab6d5bd823b798af195420319cb5c9b571e00db7d83df2a0fe1f4dbf')

prepare() {
  cd $_pkgname-$pkgver
  patch -p1 -i ../0002-vaapi-Use-libva2-message-callbacks.patch
  patch -p1 -i ../0003-demux_lavf-return-AVERROR_EOF-on-file-end.patch
  install -m755 "$srcdir"/waf-$_waf_version waf
}

build() {
  cd $_pkgname-$pkgver

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-dvdnav \
    --enable-zsh-comp

  ./waf build
}

package() {
  cd $_pkgname-$pkgver

  ./waf install --destdir="$pkgdir"

  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
