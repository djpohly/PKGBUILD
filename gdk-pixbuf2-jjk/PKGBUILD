# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gdk-pixbuf2
pkgname=$_pkgname-jjk
pkgver=2.36.0
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gtk-doc' 'gobject-introspection')
provides=("$_pkgname=$pkgver")
conflicts=($_pkgname)
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz
        gdk-pixbuf-query-loaders.hook)
sha256sums=('85ab52ce9f2c26327141b3dcf21cca3da6a3f8de84b95fa1e727d8871a23245c'
            '963afcfacde9fd36031d93c8aa4c94a669ab1dbe9ba0d2cf17599b7d358e54c2')

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-$pkgver

  make DESTDIR="$pkgdir" install

  # hook
  install -dm755 "$pkgdir"/usr/share/libalpm/hooks
  install -Dm644 ../gdk-pixbuf-query-loaders.hook \
    "$pkgdir"/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook
}

# vim:set ts=2 sw=2 et:
