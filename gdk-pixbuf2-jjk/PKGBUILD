# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gdk-pixbuf2
pkgname=$_pkgname-jjk
pkgver=2.30.4
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gtk-doc' 'gobject-introspection')
provides=("$_pkgname=$pkgver")
conflicts=($_pkgname)
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('a13bbad0d804829b260bc901dc7d284d330c534489fc1666c126fa4555bbb051')

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
