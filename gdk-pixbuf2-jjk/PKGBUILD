# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgname=gdk-pixbuf2
pkgname=$_pkgname-jjk
pkgver=2.28.1
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gtk-doc' 'gobject-introspection')
provides=("$_pkgname=$pkgver")
conflicts=($_pkgname)
options=('!libtool')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('bea0b743fdb5c3c8e23210f73623ec5f18f9ead2522942897fe739d80b50c2bb')

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
