# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-jjk
pkgname=('libgl-jjk' 'ati-dri-jjk')
pkgver=9.1
pkgrel=2
arch=('i686' 'x86_64')
makedepends=('glproto' 'libdrm' 'libxxf86vm' 'libxdamage' 'libx11' 'dri2proto'
             'python2' 'libxml2')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(LICENSE
        "ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2")
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a'
         'd3891e02215422e120271d976ff1947e')

build() {
    cd ${srcdir}/?esa-*

    autoreconf -vfi

    ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r600,swrast \
    --with-dri-drivers=radeon,swrast \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa

    make
}

package_libgl-jjk() {
    pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"
    depends=('libdrm' 'libxxf86vm' 'libxdamage' 'systemd')
    provides=('libglapi' "libgl=${pkgver}" 'libgbm' 'libgles' 'libegl'
              'khrplatform-devel')
    conflicts=('libglapi' 'libgl' 'libgbm' 'libgles' 'libegl'
               'khrplatform-devel')
    replaces=('libglapi' 'libgl' 'libgbm' 'libgles' 'libegl'
               'khrplatform-devel')

    make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/glx DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/gbm DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/egl DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/mapi/es1api DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/mapi/es2api DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install
    make -C ${srcdir}/?esa-*/src/gallium/targets/egl-static DESTDIR="${pkgdir}" install

    install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
    install -m644 ${srcdir}/?esa-*/src/mesa/gl.pc "${pkgdir}/usr/lib/pkgconfig/"

    install -m755 -d "${pkgdir}/usr/include/KHR"
    install -m644 ${srcdir}/?esa-*/include/KHR/khrplatform.h "${pkgdir}/usr/include/KHR/"

    install -m755 -d "${pkgdir}/usr/share/doc/libegl"
    install -m644 ${srcdir}/?esa-*/docs/egl.html "${pkgdir}/usr/share/doc/libegl/"

    # See FS#26284
    install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
    ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

    install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
    install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl/"
}

package_ati-dri-jjk() {
  pkgdesc="Mesa DRI radeon + Gallium3D r600 drivers for AMD/ATI Radeon"
  depends=("libgl=${pkgver}")
  conflicts=('xf86-video-ati<6.9.0-6' 'ati-dri')
  provides=('ati-dri')
  replaces=('ati-dri')

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # classic mesa drivers for radeon
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/radeon DESTDIR="${pkgdir}" install
  # gallium3D driver for r600
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-r600 DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
}
