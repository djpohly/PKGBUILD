# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-jjk
pkgname=('ati-dri-jjk' 'mesa-jjk' 'mesa-libgl-jjk')
pkgver=10.6.1
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto'
             'dri3proto' 'presentproto' 'libxxf86vm' 'libxdamage' 'elfutils'
             'systemd' 'libxshmfence' 'python2-mako')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=(ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/mesa-${pkgver}.tar.xz{,.sig}
        LICENSE)
validpgpkeys=('CEF8DDC8019CA66D4E3094F3600233BA9E54DC61'
              '8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D')
sha256sums=('6c80a2b647e57c85dc36e609d9aed17f878f0d8e0cf9ace86d14cf604101e1eb'
            'SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

build() {
    cd ${srcdir}/?esa-*

    #autoreconf -vfi # our automake is far too new for their build system :)

    ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r600,swrast \
    --with-dri-drivers=radeon,swrast \
    --with-egl-platforms=x11,drm \
    --with-sha1=libgcrypt \
    --enable-egl \
    --enable-gbm \
    --enable-gallium-llvm \
    --enable-shared-glapi \
    --enable-glx \
    --enable-glx-tls \
    --enable-dri \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float

    make
    # fake installation
    mkdir $srcdir/fakeinstall
    make DESTDIR=${srcdir}/fakeinstall install
}

package_ati-dri-jjk() {
  pkgdesc="Mesa drivers for AMD/ATI Radeon"
  depends=("mesa-libgl-jjk=${pkgver}")
  conflicts=('xf86-video-ati<6.9.0-6' 'ati-dri' 'mesa-dri')
  provides=('ati-dri' 'mesa-dri')
  replaces=('ati-dri' 'mesa-dri')

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/{r600,radeon}_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
}

package_mesa-jjk() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'systemd' 'elfutils'
           'libgl' 'libgcrypt')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('libglapi' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel'
            "mesa=${pkgver}")
  conflicts=('libglapi' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel' 'mesa')
  replaces=('libglapi' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel' 'mesa')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  install -m755 -d ${pkgdir}/usr/lib/mesa
  # move libgl/EGL/glesv*.so to not conflict with blobs - may break .pc files ?
  mv -v ${pkgdir}/usr/lib/libGL.so* ${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libEGL.so* ${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libGLES*.so* ${pkgdir}/usr/lib/mesa/

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_mesa-libgl-jjk() {
  pkgdesc="Mesa 3-D graphics library"
  depends=('mesa')
  provides=('libgl')
  replaces=('libgl')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s /usr/lib/mesa/libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so.1.2.0
  ln -s libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so.1
  ln -s libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so

  ln -s /usr/lib/mesa/libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so.1.0.0
  ln -s libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so.1
  ln -s libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so

  ln -s /usr/lib/mesa/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0
  ln -s libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so.1
  ln -s libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so

  ln -s /usr/lib/mesa/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so.2.0.0
  ln -s libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so.2
  ln -s libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"
}
