# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
_pkgname=cryptsetup
pkgname=$_pkgname-jjk
pkgver=1.6.3
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using dm-crypt"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'util-linux')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${_pkgname}-${pkgver}.tar.bz2
        encrypt_hook
        encrypt_install
        sd-encrypt
        patch)
md5sums=('a7aeb549a543eeac433eadfb6bc67837'
         'c279d86d6dc18322c054d2272ebb9e90'
         '21c45f9cab3e0b5165f68358884fbd0f'
         '6cf7e170ecd13e42fe829209628fdb4d'
         'ad7813353c7c4b12b9184366cd802aac')

prepare() {
  cd $_pkgname-$pkgver
  patch -p0 -i ../patch
  autoreconf
}

build() {
  cd "${srcdir}"/$_pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static --enable-cryptsetup-reencrypt \
      --disable-gcrypt-pbkdf2
  make
}

package() {
  cd "${srcdir}"/$_pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
  install -D -m644 "${srcdir}"/sd-encrypt "${pkgdir}"/usr/lib/initcpio/install/sd-encrypt

  # usrmove
  cd "$pkgdir"/usr
  mv sbin bin
}
