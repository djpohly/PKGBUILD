# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgname=xorg-xinit
pkgname=$_pkgname-jjk
pkgver=1.4.0
pkgrel=1
pkgdesc="X.Org initialisation program"
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'inetutils')
makedepends=('xorg-util-macros')
optdepends=('xorg-twm' 'xterm' 'xorg-xrdb' 'xorg-xmodmap')
conflicts=($_pkgname)
provides=($_pkgname)
backup=('etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2{,.sig}
        06_move_serverauthfile_into_tmp.diff
        fs25361.patch
        fs46369.patch
        xserverrc)
options=(!emptydirs)
validpgpkeys=('7C2608F8E5F9E78D7A3F26FF177544B49AF313FB' # Hans de Goede <jwrdegoede@fedoraproject.org>
              '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
              )
sha256sums=('230835eef2f5978a1e1344928168119373f6df1d0a32c09515e545721ee582ef'
            'SKIP'
            'a5c6ced5822bbb3145e0f30a4b12995782ca6d80ff2d927851ec91b5e41212cb'
            '866a478630d31bbf01c7fe7cd2a6db3912b67f693c6417d77536a5bd8d169ddd'
            '6e2c44b997e604db167f00168e8b6c68257be50447d7a80856b4aa910f52351e'
            '86835010b5350c41035495b7b845db0eecf1e4b7eeaf152b12a881fa502e5387')

prepare() {
  cd xinit-${pkgver}
  patch -Np1 -i ../06_move_serverauthfile_into_tmp.diff
  patch -Np1 -i ../fs25361.patch
  patch -Np1 -i ../fs46369.patch
  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
}

build() {
  cd xinit-${pkgver}
  autoreconf -fi
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd xinit-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
